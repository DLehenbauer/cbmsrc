.PAG 'DECIMAL MULTIPLY'
;**DECIMAL MULTIPLY FAC=FAC*ARG**
;     12-20-79
DMULT	LDX #RESMSD-RESLSD+1
	LDA #0
DMUL10	STA RESEXP,X
	DEX
	BPL DMUL10
;
;EXTRACT LSD OF FAC INTO COUNT
;
DMUL20	LDA FACLSD
	AND #$F
	STA COUNT
;
;ADD ARG TO RES COUNT TIMES
;
DMUL30	DEC COUNT
	BMI DMUL50
;
	LDX #0
	LDY #ARGMSD-ARGLSD
	SED
	CLC
DMUL40	LDA RESLSD,X
	ADC ARGLSD,X
	STA RESLSD,X
	INX
	DEY
	BPL DMUL40
;
	LDA #0
	ADC RESEXP
	STA RESEXP      ;OVERFLOW TO RESEXP
	CLD
	BCC DMUL30
;
;SHIFT RES RIGHT 1 DIGIT
;
DMUL50	LDY #3
DMUL52	LDX #RESMSD-RESLSD
	LSR RESEXP
DMUL54	ROR RESLSD,X
	DEX
	BPL DMUL54
;
;SHIFT FAC RIGHT 1 DIGIT
;
	LDX #FACMSD-FACLSD
	CLC
	LDA #0
DMUL58	ROR FACLSD,X
	ORA FACLSD,X
	DEX
	BPL DMUL58
	DEY
	BPL DMUL52
;
;CONTINUE UNTIL FAC IS ZERO
;
	TAX
	BNE DMUL20
;
;COMPUTE SIGN OF PRODUCT
;
DMULDN	LDA ARGEXP
	AND #$01
	EOR FACEXP
	STA FACEXP
;
;MOV PRODUCT FROM RES TO FAC
;
	LDX #FACMSD-FACLSD
DMUL70	LDA RESLSD,X
	STA FACLSD,X
	DEX
	BPL DMUL70
;
;COMPUTE EXPONENT OF PRODUCT
;
	SEC
	LDA ARGEXP
	AND #$FE
	PHA
	EOR FACEXP
	BPL DMUL90
;
;DIFFERENT SIGNS--DO ADD
;
	PLA
	ADC FACEXP
	CLC
	ADC #1
	JMP DMUL97
;
;BOTH NEGATIVE--WATCH UNDERFLOW
;
DMUL90	PLA
	BPL DMUL95
	ADC FACEXP
	CLC
	ADC #1
	BMI DMUL97
	JMP DZEROF
;
;BOTH POSITIVE--POSSIBLE OVERFLOW
;
DMUL95	ADC FACEXP
	CLC
	ADC #1
	BPL DMUL97
	JMP OVERR
DMUL97	STA FACEXP
	JMP DNORMZ
.END
