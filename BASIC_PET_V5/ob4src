.PAG 'OB4SRC'
CHKIN	PHA
	TXA
	PHA
	TYA
	PHA
	LDA #0
	STA SATUS
	TXA
	JSR JLTLK
	BEQ JX310
FNFERR	;FILE NOT FOUND ERROR
JX300	LDX #ERR21
JX305	JMP ERROR
JX310	JSR JZ100
	LDA FA
	BEQ JX320
	CMP #3
	BEQ JX320
	BCS JX330
	LDX SA
	CPX #$60
	BEQ JX320
	LDX #ERR24
	BNE JX305
JX320	STA DFLTN
	JMP RSTOR
JX330	PHA
	JSR OLDCLR      ;KILL DS$
JX3301	JSR TALK
	LDA SA
	BPL JX340
	JSR TKATN
	JMP JX350
JX340	JSR TKSA
JX350	LDA SATUS
	BPL *+5
	JMP OP37
	PLA
	JMP JX320
CHKOUT
CKOUT	PHA
	TXA
	PHA
	TYA
	PHA
	LDA #0
	STA SATUS
	TXA
	JSR JLTLK
	BNE JX300
	JSR JZ100
	LDA FA
	BNE CK10
	LDX #ERR25      ;NOT OUTPUT FILE
	BNE JX305
CK10	CMP #3
	BEQ JX360
	BCS JX370
	LDX SA
	CPX #$60
	BNE JX360
	LDX #ERR25
	BNE JX305
JX360	STA DFLTO
	JMP RSTOR
JX370	PHA
	JSR OLDCLR      ;KILL DS$
JX3701	JSR LISTN
	LDA SA
	BPL JX380
	JSR SCATN
	BNE JX390
JX380	JSR SECND
JX390	LDA SATUS
	BPL *+5
	JMP OP37
	PLA
	JMP JX360
JTP20	JSR ZZZ
	LDX FA
	INC BUFPT-1,X
	LDY BUFPT-1,X
	CPY #BUFSZ
	RTS
;!!!!!!!!!!!!! CASSETTE ROUTINES
;STAYS IN ROUTINE TILL PLAY SWITCH
CSTE1	JSR CS10
	BEQ CS25
	LDY #MS7-MS1    ;"PRESS PLAY"
CS30	JSR MSG
	LDY #MS9-MS1    ;"ON CASSETTE #"
	JSR MSG
	LDA FA
	ORA #'0
	JSR PRT         ;PRINT CASSETTE #
CS40	JSR TSTOP       ;WATCH FOR STOP KEY
	JSR CS10        ;WATCH CASSETTE SWITCHES
	BNE CS40
	LDY #MS18-MS1   ;"OK"
	JMP MSG
;SUBR RETURNS <> FOR CASSETTE SWITCH
CS10	LDA #$10        ;ASSUME CASSETTE #1
	LDX FA
	DEX             ;REALLY CASSETTE 1
	BEQ CS20        ;YES. . .
	LDA #$20        ;NO. . .CASSETTE #2
CS20	BIT PIAL        ;CLOSED
	BNE CS25        ;NO. . .
	BIT PIAL        ;CHECK AGAIN TO DEBOUNCE
CS25	RTS
;CHECKS FOR PLAY & RECORD
CSTE2	JSR CS10
	BEQ CS25
	LDY #MS7-MS1    ;"PRESS PLAY AND"
	JSR MSG 
	LDY #MS8-MS1    ;"RECORD"
	BNE CS30
RBLK	LDA #0
	STA SATUS
	STA VERCK
	JSR LDAD1
TRD	JSR TWAIT
	JSR CSTE1
	SEI
	LDA #0
	STA RDFLG
	STA SNSW1
	STA CMP0
	STA PTR1
	STA PTR2
	STA DPSW
	LDX FA
	DEX
	BEQ TRD2
	LDA #$90
	STA IER
	BNE TRD3
TRD2	INC PIAL1
TRD3	LDX #14
	BNE TAPE
WBLK	JSR LDAD1
TWRT	JSR TWAIT
	LDA #20
	STA SHCNH
TWRT2	JSR CSTE2
	SEI
	LDA #$A0
	STA IER
	LDX #8
TAPE	JSR BSIV
	LDA #2
	STA FSBLK
	JSR NEWCH
	DEC PIAS
	LDX FA
	DEX
	BNE TP20
	LDA #$34
	STA PIAS
	STA CAS1
	BNE TP30
TP20	LDA PIA
	STX CAS2
	AND #$EF
	STA PIA
TP30	LDX #$FF
TP32	LDY #$FF
TP35	DEY
	BNE TP35
	DEX
	BNE TP32
	STA T2H
	CLI
TP40	LDA #>KEY
	CMP CINV+1
	BEQ TP50
	JSR TSTOP
	BIT PIAS
	BPL TP40
	BIT PIAK
	JSR UDTIM
	JMP TP40
TP50	RTS
TWAIT	JSR TSTOP
	LDA #>KEY
	CMP CINV+1
	BNE TWAIT
	RTS
TSTOP	JSR STOP1
	BNE STOP3
	JSR TNIF
	JSR JX770
	STA CHANL
STOP3	JMP BSTOP
STT1	STX TEMP
	LDA CMP0
	ASL A
	ASL A
	CLC
	ADC CMP0
	CLC 
	ADC TEMP
	STA TEMP
	LDA #0
	BIT CMP0
	BMI STT2
	ROL A
STT2	ASL TEMP
	ROL A
	ASL TEMP
	ROL A
	TAX
STT3	LDA T2L
	CMP #21
	BCC STT3
	ADC TEMP
	STA TIL
	TXA
	ADC T2H
	STA TIH
	CLI
	RTS
.END
