.PAG 'TRACE-BAS/EDT'
; BASIC - TOKENS & SPACES CRUNCHED
;
; EDITOR -TOKENS & SPACES ALLOWED
;
BASIC	LDX #$FF        ;TURN ON CRUNCHER
	.BYTE $2C       ;SKIP TWO BYTES
EDITOR	LDX #00         ;TURN OFF CRUNCHER
	JSR PRTRTN      ;CHK SYNTAX
	STX CRNCH
	JMP READY
.PAG 'TRACE-TRACE'
;
; TRACE BASIC
;
TRACE2	LDA TXTPTR+1    ;CHECK FOR DIRECT
	CMP #>RAMLOC
	BCS TRC010
	JMP NGONE       ;IT IS DIRECT NO TRACE
TRC010	JSR SWAPST      ;SWAP SCREEN AND TRACE
	JSR LNNPRT      ;PRINT LINE NUMBER
;
	LDA BITABL+1    ;KILL SCROLL INFO
	AND #$0F        ;MAKE TO FIVE SINGLE LINES
	STA BITABL+1
;
	JSR CHRGET      ;PROCESS INFO
	PHA
	TAX             ;SET FLAGS
	BEQ TRC070      ;EXIT RESET
	BMI TRC040
;
TRC020	LDY #0          ;PRINT ALPHA
TRC030	LDA (TXTPTR)Y
	BEQ TRC070
	BMI TRC040
	JSR OUTDO
	INY
	BNE TRC030      ;BRA
;
TRC040	JSR RESSCH      ;LOOK UP AND PRINT TOKEN
TRC050	INY             ;PRINT IT OUT
	LDA (INDEX)Y
	BMI TRC060
	JSR OUTDO
	JMP TRC050
;
TRC060	AND #$7F        ;LAST BYTE OF TOKEN
	JSR OUTDO
;
TRC070	JSR CRDO        ;SCROLL UP ONE
	JSR SWAPST      ;SWAP POINTERS BACK
	JSR SLOW
	PLA
	SEC             ;CARRY MUST BE SET ON RETURN
	JMP NGONE1      ;RETURN TO EXECUTE
;
SWAPST	LDX #3          ;SWAP SCREEN WINDOW
SWAPS1	LDY TRCBDY,X
	LDA SCTOP,X
	STY SCTOP,X
	STA TRCBDY,X
	DEX
	BPL SWAPS1
	LDX PNTR        ;SWAP COL
	LDA TCPNTR
	STX TCPNTR
	STA PNTR
	LDX TBLX        ;SWAP LINE
	LDA TCTBLX
	STX TCTBLX
	STA TBLX
	JMP STUPT       ;SET IT AT LINE & COL
;
; PAFOT0 - PLACE A 2-BYTE NUMBER IN FBUFFER
;   .X - LOW BYTE
;   .A - HIGH BYTE
; PAFOT1 - HAS BINARY VALUE ALREADY IN FAC
;   OUTPUT - ASCII NUMBER IN FBUFFR
;
PAFOT0	STA FACHO       ;LINE # OUT
	STX FACHO+1
;
PAFOT1	LDX #0          ;ENTRY WITH FACHO AND FACHO+1 SET UP
	STX TARGET
	LDX #$90        ;CONVERT
	SEC
	JSR FLOATC
	JMP FOUTC       ;CALL BINARY FOUT
;
;
SLOW	LDA #$10        ;SLOW ALL DOWN
	LDX TRCE        ;AT THIS SPEED
SLW100	DEX
	BNE SLW200
	RTS
SLW200	STA TIH         ;START TIMMER
SLW300	BIT IFR
	BVC SLW300
	BVS SLW100
.PAG 'TRACE-AUTO'
;
; AUTO LINE NUMBER
;
; SYNTAX
;
; AUTO INC# - ENABLE AUTO
; AUTO - DISABLE AUTO
;
AUTO	JSR LINGET      ;GET START #
	JSR PRTRTN      ;CHK SYNTAX
	LDA LINNUM      ;SAVE IT
	STA AUTINC
	LDA LINNUM+1
	STA AUTINC+1
	JMP NOTDEL      ;FINSH WITH READY
;
; INSERTS NEW LINES AND DOES AUTO
;
WNUMB	JSR LINGET      ;GET LINE # INTO LINNUM
	BEQ WNB900      ;LINE # ONLY
WNB400	LDA AUTINC      ;IN AUTO MODE ?
	ORA AUTINC+1
	BEQ WNB900      ;NO
WNB600	LDA LINNUM      ;CALC # FOR NEXT LINE
	CLC
	ADC AUTINC
	TAX             ;SET UP FOR PAFOT0
	LDA LINNUM+1
	ADC AUTINC+1
	JSR PAFOT0      ;OUTPUT NUMBER
	LDX #0          ;PUT ASCII IN KEYB BUFFER
WNB700	LDA FBUFFR,X
	BEQ WNB800      ;DONE
	STA KEYD,X
	INX
	BNE WNB700      ;ALWAYS
WNB800	LDA #$20        ;1 SPACE AFTER
	STA KEYD,X
	INX
	STX NDX         ;# CHAR PUT IN BUF
WNB900	RTS             ;SQUISH IT
.END
