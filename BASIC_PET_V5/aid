.PAG 'AID-MIXED'
; BASIC - TOKENS & SPACES CRUNCHED
;
; EDITOR -TOKENS & SPACES ALLOWED
;
BASIC	LDX #$FF        ;TURN ON CRUNCHER
	.BYTE $2C       ;SKIP TWO BYTES
EDITOR	LDX #00         ;TURN OFF CRUNCHER
	JSR PRTRTN      ;CHK SYNTAX
	STX CRNCH
	JMP READY
PRTRTN	JSR CHRGOT      ;CHECK SYNTAX
	BNE RNGERR      ;SYNTAX ERROR ELSE READY
	RTS
;
; PAFOT0 - PLACE A 2-BYTE NUMBER IN FBUFFER
;   .X - LOW BYTE
;   .A - HIGH BYTE
; PAFOT1 - HAS BINARY VALUE ALREADY IN FAC
;   OUTPUT - ASCII NUMBER IN FBUFFR
;
PAFOT0	STA FACHO       ;LINE # OUT
	STX FACHO+1
;
PAFOT1	LDX #0          ;ENTRY WITH FACHO AND FACHO+1 SET UP
	STX TARGET
	LDX #$90        ;CONVERT
	SEC
	JSR FLOATC
	JMP FOUTC       ;CALL BINARY FOUT
;
;
; INPUT RANGE PARMS
;
; 100-200
; 100-
; -200
; 100
;
RANGE	BCC RNG100      ;FROM CHRGOT, A # ?
	BEQ RNG100      ;NO - END OF LINE
	CMP #'-         ;A DASH
	BEQ RNG100
	CMP #MINUTK     ;TOKEN '-'
	BEQ RNG100
RNGERR	JMP SNERR       ;SYNTAX ERROR
RNG100	JSR LINGET      ;GET #
	JSR FNDLIN      ;FIND LINE OR SET PTRS
	JSR CHRGOT      ;GET LAST CHAR
	BEQ RNG200      ;SKIP END OF LINE
	CMP #'-         ;A DASH ?
	BEQ RNG150      ;YES
	CMP #MINUTK     ;TOKEN DASH?
	BNE RNGERR      ;NO - BAD SYNTAX
RNG150	JSR CHRGET      ;YES - SKIP DASH
	JSR LINGET      ;GET TO #
	BNE RNGERR      ;NOT A NUMBER ERROR
RNG200	LDA LINNUM
	ORA LINNUM+1    ;A TO # ?
	BNE RNGRTS      ;YES - THE DONE
	LDA #$FF        ;NO - MAKE MAX #
	STA LINNUM
	STA LINNUM+1
RNGRTS	RTS
;
; AID COMMAND
;
; SHOW LAST EXECUTE CHRGOT
;
AID	JSR CRDO        ;PRINT (CR) (LF)
	LDA #$80        ;ENABLE AID
	STA AIDFLG
	LDA CURLIN      ;GET LAST LINE #
	LDX CURLIN+1
LLLL1	STA LINNUM
	STX LINNUM+1
	JSR FNDLIN      ;FIND IT
LLLL0	JMP LIST4       ;TRY THIS OUT
;
TRACE
DELETE
CHANGE
FIND
RENUM
;
ITSMIS	JSR DDIREC      ;IF DIRECT...
	BNE ITSWNU      ;...NOT THEN NO ACTION
	LDA #<ITGONE
	LDY #>ITGONE
	JSR STROUT
	JMP READY
ITSWNU	RTS             ;ELSE NORMAL RETURN
;
ITGONE	.BYT $D,'LOAD SYSAID PROGRAM',$00
.PAG 'TRACE-AUTO'
;
; AUTO LINE NUMBER
;
; SYNTAX
;
; AUTO INC# - ENABLE AUTO
; AUTO - DISABLE AUTO
;
AUTO	JSR LINGET      ;GET START #
	JSR PRTRTN      ;CHK SYNTAX
	LDA LINNUM      ;SAVE IT
	STA AUTINC
	LDA LINNUM+1
	STA AUTINC+1
	JMP READY       ;FINSH WITH READY
;
; INSERTS NEW LINES AND DOES AUTO
;
WNUMB	JSR LINGET      ;GET LINE # INTO LINNUM
	BEQ WNB900      ;LINE # ONLY
WNB400	LDA AUTINC      ;IN AUTO MODE ?
	ORA AUTINC+1
	BEQ WNB900      ;NO
WNB600	LDA LINNUM      ;CALC # FOR NEXT LINE
	CLC
	ADC AUTINC
	TAX             ;SET UP FOR PAFOT0
	LDA LINNUM+1
	ADC AUTINC+1
	JSR PAFOT0      ;OUTPUT NUMBER
	LDX #0          ;PUT ASCII IN KEYB BUFFER
WNB700	LDA FBUFFR,X
	BEQ WNB800      ;DONE
	STA KEYD,X
	INX
	BNE WNB700      ;ALWAYS
WNB800	LDA #$20        ;1 SPACE AFTER
	STA KEYD,X
	INX
	STX NDX         ;# CHAR PUT IN BUF
WNB900	RTS             ;SQUISH IT
.END
