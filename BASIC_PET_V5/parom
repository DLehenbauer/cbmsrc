.PAGE 'PROG AID ROM'
PAROM	.BYT 'PA',0,1   ;ROM ID AND VERSION
;
; PAROMC - IGONE VECTORED HERE IF ROM IN SYSTEM
;  DECIDE IF WE HAVE AN AID COMMAND
;  IF YES THEN DO ELSE RETURN TO OLD
;
PAROMC	JSR CHRGET      ;GET TOKEN
;
; IF USER ADDS OWN LIST OF DISPATCHES THEN RETURN HERE
;  THE USER SHOULD ALWAYS RETURN TO IGONE+3
;
PAROMD	PHP             ;SAVE FLAGS
	CMP #RENUTK     ;CHECK TO SEE IF IN RANGE
	BCC PAROMF      ;NO LEAVE
	CMP #BASTK
	BCC PAROMG      ;YES DO
PAROMF	PLP             ;RESTORE FLAGS
	JMP NGONE1      ;NOT A PA TOKEN RETURN TO NGONE+3
PAROMG	SBC #RENUTK-1   ;GET INDEX
	ASL A
	PLP             ;TOSS FLAGS
	TAY
	LDA PADSP+1,Y   ;PUSH ADDRESS ONTO STACK
	PHA
	LDA PADSP,Y
	PHA
	JMP CHRGET
;
PADSP	;PROGRAMMER AID COMMAND DISPATCH
	.WOR RENUMX-1
	.WOR DELETX-1
	.WOR TRACEX-1
	.WOR CHANGX-1
	.WOR FINDXX-1
;
.END
