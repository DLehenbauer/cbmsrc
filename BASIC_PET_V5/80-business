.PAG '80-BUSINESS 6/03/80'
;*****************************************************************
;
;  80 COLUMN BUSINESS SCREEN EDITOR WITH SCREEN LIMITED LINE WRAP
;                6/03/80
;
;*****************************************************************
BEGIN=*
	*=BEGIN
SCNRAM=$8000
LLEN=80
SCYMAX=24
SCXMAX=79
.SKI 3
; ****** JUMP TABLE ******
;
JCINT	JMP CINT
JLP2	JMP LP2
JLOOP5	JMP LOOP5
JPRT	JMP PRT
JPULS	JMP PULS
JKEY	JMP KEY
JPREND	JMP PREND
JCLSR	JMP CLSR
JTXCRT	JMP TXCRT
JGRCRT	JMP GRCRT
JCSET	JMP CRTSET
JSCRDN	JMP SCRDWN
JSCRUP	JMP SCRUP
JSCNKY	JMP SCNKEY
JBELL	JMP BELL
JSRPT	JMP SETRPT
	JMP TOPLF
	JMP BOTRT
;
;  SCROLL UP AND DOWN INDIRECTS
;
SCROLU	JMP (UPINDR)
SCROLD	JMP (DNINDR)
.SKI 3
SETRPT	STA RPTFLG
	RTS
.SKI 3
	*=BEGIN+$004B
;
CINT	JSR EXTRA0      ;EXTRA SETUP STUFF
	JSR TXCRT       ;PUT IN TEXT MODE
CLSR	LDX SCTOP       ;START AT TOP OF WINDOW
	DEX
CLS10	INX             ;NEXT LINE
	JSR SCRLY       ;SET PNT TO BEGIN OF LINE
	JSR CLRLN       ;CLEAR THE LINE
	CPX SCBOT       ;DONE ?
	BCC CLS10       ;NO
	LDX #3          ;CLEAR SCREEN WRAP BIT MAP
	LDA #0
NOBITS	STA BITABL,X
	DEX
	BPL NOBITS
NXTD	LDX SCTOP       ;MOVE TO TOP
	STX TBLX
STU10	LDY SCLF        ;LEFT OF THE SCREEN WINDOW
	STY PNTR
	STY LSTP
STUPT	LDX TBLX        ;RESET PTR TO LINE BEGIN
	JMP SCRSET      ;SET PNT TO BEGIN OF LINE
.SKI 2
; SET PNT TO BEGINNING OF THE LINE
;
SCRLY	LDY SCLF        ;SET COLUMN TO LEFT
	DEY
SCRSET	LDA LDTB2,X
	STA PNT
	LDA LDTB1,X
	STA PNT+1
	RTS
.SKI 2
;**** SET GRAPHIC MODE ****
;
TXCRT	LDA #<TEXT      ;SET 6845 TO TEXT
	LDX #>TEXT
	LDY #$0E        ;TEXT CHAR SET
	BNE CRTSET
GRCRT	LDA #<GRAF      ;SET 6845 TO GRAPHICS
	LDX #>GRAF
	LDY #$0C        ;GRAPHIC CHAR SET
CRTSET	STA SAL         ;SET PTR AT TABLE
	STX SAH
	LDA PCR
	AND #$F0        ;CHANGE CA2 ONLY
	STA FNLEN       ;SAVE TEMP
	TYA
	ORA FNLEN       ;ADD CHAR SET
	STA PCR
	LDY #17         ;17 BYTE TABLE
CRT10	LDA (SAL),Y
	STY CRTCA       ;INDEX TO REG.
	STA CRTCB
	DEY
	BPL CRT10
	RTS
.SKI 2
; *** INPUT ROUTINES ***
; ++++++ MUST START @ $E0A7 ++++++
;
LP2	LDX KYNDX       ;ARE THERE ANY PGM KEYS
	BEQ LP3         ;BRANCH IF NOT
	LDY KEYIDX      ;GET INDEX TO CURRENT CHAR
	LDA (KEYPNT)Y   ;GET CURRENT BYT
	DEC KYNDX       ;1 BYTE DOWN
	INC KEYIDX      ;BUMP INDEX TO NEXT CHAR
	RTS
LP3	LDY KEYD        ;GET KEY FROM IRQ BUFFER
	LDX #0
LP1	LDA KEYD+1,X
	STA KEYD,X
	INX
	CPX NDX
	BNE LP1
	DEC NDX
	TYA
	CLI
	RTS
LOOP4	JSR PATCH2      ;GO ADD BELL SOUND
LOOP3	LDA NDX
	ORA KYNDX       ;OR IF THERE'S ANY IN THE PGM BUFFER
	STA BLNSW
	BEQ LOOP3
	SEI
	LDA BLNON       ;IS CHAR NORMAL ?
	BEQ LP21        ;YES
	LDA GDBLN       ;NO - GET ORIGINAL NUMBER
	LDY #0
	STY BLNON
	JSR DSPP        ;PUT CHAR ON SCREEN
LP21	JSR LP2
	CMP #$83
	BNE LP22
	SEI
	LDX #9
	STX NDX
LP23	LDA RUNTB-1,X
	STA KEYD-1,X
	DEX
	BNE LP23
PRE3	BEQ LOOP3
LP22	CMP #$D
	BNE LOOP4
	LDY SCRT        ;RIGHT MAX OF WINDOW
	STY CRSW
	LDA TBLX        ;WE WANT TO FIND THE END OF CURENT LINE
	STA SEDT1       ;SAVE LINE# OF CURENT LINE
	JSR FNDEND      ;CHECK NXT LINE FOR CONT
	LDA TBLX        ;SAVE ENDING DISPLAY LINE#
	STA LINTMP
	LDA SEDT4       ;RESTORE CURENT LINE INDEX
	STA TBLX
LP69	JSR FISTRT      ;FIND BEGINING OF LINE
	LDY SCLF        ;POINT TO BEGINING OF LINE
	STY PNTR
	LDY #0
	STY QTSW
	LDA LSXP
	CMP TBLX
	BNE LOP5
	LDA LSTP
	STA PNTR
	CMP INDX
	BCC LOP5
	LDA TBLX
	CMP LINTMP
	BEQ CLP2
	BNE LOP5
; ++++++ MUST START @ $E116 ++++++
;
LOOP5	TYA
	PHA
	TXA
	PHA
	JMP (VINP)      ;GO THRU INDIRECT
LOP55	LDA CRSW        ;ENTRY FROM INDIRECT
	BEQ PRE3
LOP5	LDY PNTR
	LDA (PNT)Y
	STA DATA
LOP51	AND #$3F
	ASL DATA
	BIT DATA
	BPL LOP54
	ORA #$80
LOP54	BCC LOP52
	LDX QTSW
	BNE LOP53
LOP52	BVS LOP53
	ORA #$40
LOP53	JSR QTSWC
	STY T2          ;SAVE CURRENT CHAR INDEX
	LDY TBLX
	STY SEDT4
	JSR NXTCHR
	LDY T2
	CPY INDX
	BNE CLP1
	LDX SEDT4
	CPX LINTMP      ;ARE WE AT THE LAST LINE?
	BNE CLP1        ;BRANCH IF NOT
	LDX SEDT1       ;GET THE LINE WE WERE ORIGINALY ON
	STX TBLX        ;AND PRINT A CARRIAGE RETURN FROM HERE
CLP2	LDA #0
	STA CRSW
	LDA #$D
	LDX DFLTN       ;FIX GETS FROM SCREEN
	CPX #3          ;IS IT THE SCREEN?
	BEQ CLP2A
	LDX DFLTO
	CPX #3
	BEQ CLP21
CLP2A	JSR PRT
CLP21	LDA #$D
CLP1	STA DATA
	PLA
	TAX
	PLA
	TAY
	LDA DATA
	CMP #$DE
	BNE CLP7
	LDA #$FF
CLP7	RTS
; *** TEST FOR QUOTE MODE ***
QTSWC	CMP #$22
	BNE QTSWL
	LDA QTSW
	EOR #$1
	STA QTSW
	LDA #$22
QTSWL	RTS
; *** OUTPUT CHARS ***
NXT33	ORA #$40
NXT3	LDX RVS
	BEQ NVS
NC3	ORA #$80
NVS	LDX INSRT
	BEQ NVSA
	DEC INSRT
NVSA	LDX INSFLG      ;ARE WE IN AUTO INSERT MODE?
	BEQ NVS1        ;BRANCH IF NOT
	PHA             ;SAVE THE CHAR
	JSR INSERT      ;MAKE ROOM FOR THIS CHAR
	LDX #0
	STX INSRT       ;MAKE SURE WE TURN OFF INSERT MODE.
	PLA             ;RESTORE CHAR
NVS1	JSR DSPP
JSTS	JSR NXTCHR      ;CHECK FOR A SCROLL
	BCC LFTMAX
	JSR NXTD
	BNE LOOP2       ;LETS GET OUT OF HERE
LFTMAX	LDY SCLF        ;RIGHT MAX WINDOW
	CPY PNTR
	BCC LOOP2
JJS2	JSR PUTBIT      ;MARK LINE AS CONTINUATION
	LDY SCLF        ;LEFT MIN OF WINDOW
	STY PNTR
LOOP2	LDA #0          ;RESET HOME KEY FLAG
	STA FHOME
LOPII	PLA             ;EXIT FROM PRT
	TAY
	LDA INSRT
	BEQ LOP2
	LSR QTSW
LOP2	PLA
	TAX
	PLA
	CLI
	RTS
BKLN	LDY SCRT        ;TEST FOR WRAP UP
	LDX SCTOP       ;AT TOP ?
	CPX TBLX
	BCC BKLN1       ;NO
	LDY SCLF        ;YES - STOP THERE
	STY PNTR
	PLA             ;SKIP A LEVEL OF SUBROUTINE
	PLA
	BNE LOOP2       ;ALWAYS
BKLN1	DEC TBLX
	STY PNTR
	JMP STUPT       ;SET FRONT LINE PTR
.SKI 3
; CLEAR ONE LINE
;
CLRLN	LDA #$20
CLR10	INY
	STA (PNT),Y
	CPY SCRT
	BCC CLR10
	RTS
.SKI 2
.END
