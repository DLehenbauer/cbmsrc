.PAG 'INITIALIZATION CODE'
INIT	LDX #STKEND-256
	TXS
	LDA #$4C
	STA JMPER
	STA USRPOK
	LDA #<FCERR
	LDY #>FCERR
	STA USRPOK+1
	STY USRPOK+2
	LDX #11         ;RND AND INDIRECT INITS
MOVCHG	LDA RNDINT-1,X
	STA RNDX-1,X
	DEX
	BNE MOVCHG
	LDA #STRSIZ
	STA FOUR6
	TXA
	STA BITS
	STA CHANNL
	STA LASTPT+1
	STA DSDESC
	PHA
	INX
	STX BUF-3
	STX BUF-4
	LDX #TEMPST
	STX TEMPPT
	LDY #>RAMLOC
	STA TXTTAB
	STY TXTTAB+1
	STA LINNUM
	STY LINNUM+1
	TAY
.SKI
;CREATE RAM INITIALIZATION IF NEEDED
	LDA #>OFFSET
	BNE LOOPMM
	LDA #<ROMLOC
	LDY #>ROMLOC
	JMP USEDEF
LOOPMM	INC LINNUM
	BNE LOOPM1
	INC LINNUM+1
	BMI USEDEC
LOOPM1	LDA #85
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BNE USEDEC
	ASL A
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BEQ LOOPMM
USEDEC	LDA LINNUM
	LDY LINNUM+1
USEDEF	STA MEMSIZ
	STY MEMSIZ+1
	LDA MEMSIZ      ;INITIALIZE PROGRAMABLE KEYBUF
	SEC
	SBC MEMEND
	STA MEMSIZ
	LDA MEMSIZ+1
	SBC #0
	STA MEMSIZ+1
	STA PKYBUF+1
	LDA MEMSIZ
	STA PKYBUF
	DEC MEMSIZ
	BPL *+4
	DEC MEMSIZ+1
	LDA MEMSIZ
	LDY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	LDY MEMEND
	LDA #0
PGM000	STA (PKYBUF)Y   ;ZERO OUT PGM KEYS
	DEY
	BPL PGM000
	LDY #0
	LDA #$FF
	STA CRNCH       ;COME UP IN BASIC
	JSR PUDECL      ;SET UP PRINT USING DEFINES
	TYA
	STA AUTINC      ;CLR AUTO GUYS
	STA AUTINC+1    ;
	STA TRCE        ;DISABLE TRACE
	STA EDSAV       ;DISABLE EDITOR SAVE
	STA AIDFLG      ;DISABLE AID
	STA TARGET      ;BIN MODE POWER ON
	STA CASS1       ;CLR CASS FOR USE
	STA CASS2       ;BOTH
	STA USINGV      ;CLR PRINT USING POINTER
	STA (TXTTAB)Y
	INC TXTTAB
	LDA #<FREMES
	LDY #>FREMES
	JSR STROUT
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	JSR SCRTCH
	JMP READY
;
PUDECL	LDA #'*         ;SET UP PRINT USING DEFINES
	STA FILLCH
	LDA #'$
	STA MONEY
	LDA #'.
	STA TENTHS
	LDA #',
	STA THOUSD
	RTS
;
.END
