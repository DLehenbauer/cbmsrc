;*****************************************************************
;
;  40 COLUMN GRAPHIC  SCREEN EDITOR WITH SCREEN LIMITED LINE WRAP
;                4/28/80
;
;*****************************************************************
.PAG '40-GRAPHIC 4/28/80'
BEGIN=*
	*=BEGIN
SCNRAM=$8000
LLEN=40
SCYMAX=24
SCXMAX=39
.SKI 3
; ****** JUMP TABLE ******
;
JCINT	JMP CINT
JLP2	JMP LP2
JLOOP5	JMP LOOP5
JPRT	JMP PRT
JPULS	JMP PULS
JKEY	JMP KEY
JPREND	JMP PREND
JCLSR	JMP CLSR
JTXCRT	JMP TXCRT
JGRCRT	JMP GRCRT
JCSET	JMP CRTSET
JSCRDN	JMP SCRDWN
JSCRUP	JMP SCRUP
JSCNKY	JMP SCNKEY
JBELL	JMP BELL
JSRPT	JMP SETRPT
	JMP TOPLF
	JMP BOTRT
;
;  SCROLL UP AND DOWN INDIRECTS
;
SCROLU	JMP (UPINDR)
SCROLD	JMP (DNINDR)
.SKI 3
SETRPT	STA RPTFLG
	RTS
.SKI 3
	*=BEGIN+$004B
;
CINT	JSR EXTRA0      ;EXTRA SETUP STUFF
CLSR	LDX SCTOP       ;START AT TOP OF WINDOW
	DEX
CLS10	INX             ;NEXT LINE
	JSR SCRLY       ;SET PNT TO BEGIN OF LINE
	JSR CLRLN       ;CLEAR THE LINE
	CPX SCBOT       ;DONE ?
	BCC CLS10       ;NO
	LDX #3          ;CLEAR SCREEN WRAP BIT MAP
	LDA #0
NOBITS	STA BITABL,X
	DEX
	BPL NOBITS
NXTD	LDX SCTOP       ;MOVE TO TOP
	STX TBLX
STU10	LDY SCLF        ;LEFT OF THE SCREEN WINDOW
	STY PNTR
	STY LSTP
STUPT	LDX TBLX        ;RESET PTR TO LINE BEGIN
	JMP SCRSET      ;SET PNT TO BEGIN OF LINE
.SKI 2
; SET PNT TO BEGINNING OF THE LINE
;
SCRLY	LDY SCLF        ;SET COLUMN TO LEFT
	DEY
SCRSET	LDA LDTB2,X
	STA PNT
	LDA LDTB1,X
	STA PNT+1
	RTS
.SKI 2
; *** INPUT ROUTINES ***
; ++++++ MUST START @ $E0A7 ++++++
;
LP2	LDY KEYD        ;GET KEY FROM IRQ BUFFER
	LDX #0
LP1	LDA KEYD+1,X
	STA KEYD,X
	INX
	CPX NDX
	BNE LP1
	DEC NDX
	TYA
	CLI
CRTSET
GRCRT
TXCRT
	RTS
LOOP4	JSR PRT         ;ECHO THE CHARACTER
LOOP3	LDA NDX
	STA BLNSW
	BEQ LOOP3
	SEI
	LDA BLNON       ;IS CHAR NORMAL ?
	BEQ LP21        ;YES
	LDA GDBLN       ;NO - GET ORIGINAL NUMBER
	LDY #0
	STY BLNON
	JSR DSPP        ;PUT CHAR ON SCREEN
LP21	JSR LP2
	CMP #$83
	BNE LP22
	SEI
	LDX #9
	STX NDX
LP23	LDA RUNTB-1,X
	STA KEYD-1,X
	DEX
	BNE LP23
	BEQ LOOP3
LP22	CMP #$D
	BNE LOOP4
	LDY SCRT        ;RIGHT MAX OF WINDOW
	STY CRSW
	JSR FNDEND      ;CHECK NXT LINE FOR CONT
	LDA TBLX        ;SAVE ENDING DISPLAY LINE#
	STA LINTMP
	CPY  SCLF       ;ARE WE AT THE LEFT MARGIN
	BNE LP69        ;BRANCH IF NOT
	JSR GETBIT      ;OPEN THE BIT FOR THIS LINE
	CLC             ;CLEAR IT - THIS IS NOT A CONTINUATION
	JSR CLOSBT      ;PUT CLEARED BIT BACK
LP69	JSR FISTRT      ;FIND BEGINING OF LINE
	LDY SCLF        ;POINT TO BEGINING OF LINE
	STY PNTR
	JSR PATCH1
	NOP
	STY QTSW
	LDA LSXP
	BMI LOP5
	CMP TBLX
	BNE LOP5
	LDA LSTP
	STA PNTR
	CMP INDX
	BCC LOP5
	LDA TBLX
	CMP LINTMP      ;ARE START AND END THE SAME?
	BEQ CLP2        ;BRANCH IF SO
	BNE LOP5        ;ELSE PROCESS IT
; ++++++ MUST START @ $E116 ++++++
;
LOOP5	TYA
	PHA
	TXA
	PHA
	JMP (VINP)      ;GO THRU INDIRECT
LOP55	LDA CRSW        ;ENTRY FROM INDIRECT
	BEQ LOOP3
LOP5	LDY PNTR
	LDA (PNT)Y
	STA DATA
LOP51	AND #$3F
	ASL DATA
	BIT DATA
	BPL LOP54
	ORA #$80
LOP54	BCC LOP52
	LDX QTSW
	BNE LOP53
LOP52	BVS LOP53
	ORA #$40
LOP53	JSR QTSWC
	STY T2          ;SAVE CURRENT CHAR INDEX
	JSR NXTCHR
	LDY T2
	CPY INDX
	BNE CLP1
	LDX TBLX
	CPX LINTMP      ;ARE WE AT THE LAST LINE?
	BNE CLP1        ;BRANCH IF NOT
	LDY SCLF        ;START AT BEGINING OF LEFT MARGIN
	STY PNTR
	JSR STUPT       ;ESTABLISH CURSOR POSITION
CLP2	LDA #0
	STA CRSW
	LDA #$D
	LDX DFLTN       ;FIX GETS FROM SCREEN
	CPX #3          ;IS IT THE SCREEN?
	BEQ CLP2A
	LDX DFLTO
	CPX #3
	BEQ CLP21
CLP2A	JSR PRT
CLP21	LDA #$D
CLP1	STA DATA
	PLA
	TAX
	PLA
	TAY
	LDA DATA
	CMP #$DE
	BNE CLP7
	LDA #$FF
CLP7	RTS
; *** TEST FOR QUOTE MODE ***
QTSWC	CMP #$22
	BNE QTSWL
	LDA QTSW
	EOR #$1
	STA QTSW
	LDA #$22
QTSWL	RTS
; *** OUTPUT CHARS ***
NXT33	ORA #$40
NXT3	LDX RVS
	BEQ NVS
NC3	ORA #$80
NVS	LDX INSRT
	BEQ NVSA
	DEC INSRT
NVSA	LDX INSFLG      ;AUTO INSERT MODE?
	BEQ NVS1        ;BRANCH IF NOT
	PHA             ;SAVE CHARACTER
	JSR INSERT      ;MAKE ROOM FOR IT
	LDX #0
	STX INSRT       ;TURN OFF INSERT MODE
	PLA             ;RESTORE CHAR
NVS1	JSR DSPP
JSTS	JSR NXTCHR      ;CHECK FOR A SCROLL
	BCC LFTMAX      ;BRANCH IF SCROLL ENABLED OR NOT AT BOT
	JSR NXTD        ;RESET TOP LEFT
LFTMAX	LDY SCLF        ;LEFT  MAX WINDOW
	CPY PNTR
	BCC LOOP2
JJS2	JSR PUTBIT      ;MARK LINE AS CONTINUATION
	LDY SCLF        ;LEFT MIN OF WINDOW
	STY PNTR
LOOP2	LDA #0          ;RESET HOME KEY FLAG
	STA FHOME
LOPII	PLA             ;EXIT FROM PRT
	TAY
	LDA INSRT
	BEQ LOP2
	LSR QTSW
LOP2	PLA
	TAX
	PLA
	CLI
	RTS
BKLN	LDY SCRT        ;TEST FOR WRAP UP
	LDX SCTOP       ;AT TOP ?
	CPX TBLX
	BCC BKLN1       ;NO
	LDY SCLF        ;YES - STOP THERE
	STY PNTR
	PLA             ;SKIP A LEVEL OF SUBROUTINE
	PLA
	BNE LOOP2       ;ALWAYS
BKLN1	DEC TBLX
	STY PNTR
	JMP STUPT       ;SET FRONT LINE PTR
.SKI 3
; CLEAR ONE LINE
;
CLRLN	LDA #$20
CLR10	INY
	STA (PNT),Y
	CPY SCRT
	BCC CLR10
	RTS
.SKI 2
PATCH1	LDY SCLF
	STY PNTR
	LDY #0
	RTS
.END
