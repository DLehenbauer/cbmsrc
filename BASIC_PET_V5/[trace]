.PAG 'TRACE'
;**********************************
;
; TRACE - DISABLE TRACE RSR 5-14-80
; TRACE # - ENABLE TRACE #=DELAY
;
;**********************************
.SKI
TRACEX	BEQ TRAC01      ;DISABLE ?
	JSR GETBYT      ;GET VALUE 0-255
	STX TRCE        ;TRACE SPEED
	LDA #0
	STA TRCBDY      ;TRACE TOP
	STA TCPNTR      ;TRACE START COL
	LDX #4          ; 5 LINES OF TRACE 0-4
	STX TRCBDY+1    ;TRACE BOTTOM
	STX TCTBLX      ;TRACE START ROW
	INX
	STX SCTOP       ;SET TOP OF MAIN SCREEN
	LDA SCLF        ;USE CURRENT EDGES - SCREEN INDEPENDENT
	STA TRCBDY+2
	LDA SCRT
	STA TRCBDY+3
	LDA #<TRACE2    ;TRACE ON
	LDX #>TRACE2
	BNE TRAC02      ;BRA
TRAC01	LDA #0          ;RESTORE TO TOP
	STA SCTOP
	LDA #<PAROMD    ;TRACE OFF
	LDX #>PAROMD
TRAC02	STA IGONE       ;SET INDIRECTS
	STX IGONE+1
	JMP PRTRTN
.PAG 'TRACE-TRACE'
;
; TRACE BASIC
;
TRACE2	LDA TXTPTR+1    ;CHECK FOR DIRECT
	CMP #>RAMLOC
	BCS TRC010
	JMP PAROMC      ;IT IS DIRECT NO TRACE
TRC010	JSR SWAPST      ;SWAP SCREEN AND TRACE
	JSR LNNPRT      ;PRINT LINE NUMBER
;
	LDA BITABL+1    ;KILL SCROLL INFO
	AND #$0F        ;MAKE TO FIVE SINGLE LINES
	STA BITABL+1
;
	JSR CHRGET      ;PROCESS INFO
	PHA
	TAX             ;SET FLAGS
	BEQ TRC070      ;EXIT RESET
	BMI TRC040
;
TRC020	LDY #0          ;PRINT ALPHA
TRC030	LDA (TXTPTR)Y
	BEQ TRC070
	BMI TRC040
	JSR OUTDO
	INY
	BNE TRC030      ;BRA
;
TRC040	JSR RESSCH      ;LOOK UP AND PRINT TOKEN
TRC050	INY             ;PRINT IT OUT
	LDA (INDEX)Y
	BMI TRC060
	JSR OUTDO
	JMP TRC050
;
TRC060	AND #$7F        ;LAST BYTE OF TOKEN
	JSR OUTDO
;
TRC070	JSR CRDO        ;SCROLL UP ONE
	JSR SWAPST      ;SWAP POINTERS BACK
	JSR SLOW
	PLA
	SEC             ;CARRY MUST BE SET ON RETURN
	JMP PAROMD      ;RETURN TO EXECUTE
;
SWAPST	LDX #3          ;SWAP SCREEN WINDOW
SWAPS1	LDY TRCBDY,X
	LDA SCTOP,X
	STY SCTOP,X
	STA TRCBDY,X
	DEX
	BPL SWAPS1
	LDX PNTR        ;SWAP COL
	LDA TCPNTR
	STX TCPNTR
	STA PNTR
	LDX TBLX        ;SWAP LINE
	LDA TCTBLX
	STX TCTBLX
	STA TBLX
	JMP STUPT       ;SET IT AT LINE & COL
;
SLOW	LDA #$10        ;SLOW ALL DOWN
	LDX TRCE        ;AT THIS SPEED
SLW100	DEX
	BNE SLW200
	RTS
SLW200	STA TIH         ;START TIMMER
SLW300	BIT IFR
	BVC SLW300
	BVS SLW100
.END
