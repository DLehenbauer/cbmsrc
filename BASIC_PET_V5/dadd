.PAG 'DECIMAL ADD-SUB'
;**DECIMAL SUBTRACT FAC=ARG-FAC**
;     1-02-80
;COMPLEMENT SIGN OF FAC MANTISSA
;
DSUB	LDA FACEXP
	EOR #$01
	STA FACEXP
;
;**DECIMAL ADD FAC=FAC+ARG**
;
;EXCHANGE ARG AND FAC
;
DADD0	LDX #FACMSD-FACEXP
;
;NO EXCHANGE IF ARG 0
;
	LDA ARGMSD
	BNE DADD2
	LDA FACEXP
	STA ARGEXP
	JMP DADD
;
DADD2	LDY ARGEXP,X
	LDA FACEXP,X
	STA ARGEXP,X
	STY FACEXP,X
	DEX
	BPL DADD2
;
;CHECK IF BOTH EXPONENTS SAME
;
DADD	LDA FACMSD
	BNE DADD5
	LDA ARGEXP
	STA FACEXP
DADD5	LDA FACEXP
	ORA #1
	PHA
	SEC             ;FOR LATER SUBTRACTS
	EOR ARGEXP
	BPL DADD10
;
;COMPUTE # OF TIMES ARG TO BE SHIFTED
;RIGHT, MAKING SURE FACEXP>=ARGEXP FOR
;CASE WHEN EXP SIGNS --DIFFERENT--
;
	PLA
	BMI DADD0       ;FACEXP<ARGEXP
	SBC ARGEXP
	JMP DADD20
;
;COMPUTE # OF TIMES ARG TO BE SHIFTED
;RIGHT, MAKING SURE FACEXP>=ARGEXP FOR
;CASE WHEN EXP SIGNS --SAME--
;
DADD10	PLA
	SBC ARGEXP
	BCC DADD0
	AND #$FE
	BNE DADD20
;
;IF FACEXP=ARGEXP, THEN MAKE SURE THAT
;ABS(FAC-MANTISSA)>=ABS(ARG-MANTISSA)
;
	PHA
	LDX #0
;CARRY SET HERE
	SED
	LDY #FACMSD-FACLSD
DADD12	LDA FACLSD,X
	SBC ARGLSD,X
	INX
	DEY
	BPL DADD12
	CLD
	PLA
	BCC DADD0
;
;CONVERT DIFFERENCE OF EXPONENTS TO SHIFT COUNT
;
DADD20	LSR A
	STA COUNT
;
;SHIFT ARG MANTISSA RIGHT THE NUMBER OF
;TIMES SPECIFIED IN COUNT.
;
DADD30	DEC COUNT
	BMI DADD40
	LDY #3
DADD32	LDX #ARGMSD-ARGLSD
	CLC
DADD34	ROR ARGLSD,X
	DEX
	BPL DADD34
	DEY
	BPL DADD32
	BMI DADD30
;
;IF BOTH MANTISSA HAVE SAME SIGN THEN
;PERFORM AN ADD :FAC=FAC+ARG
;
DADD40	LDA ARGEXP
	EOR FACEXP
	ROR A
	LDX #0
	LDY #FACMSD-FACLSD
	SED
	BCS DADD50
;
DADD42	LDA FACLSD,X
	ADC ARGLSD,X
	STA FACLSD,X
	INX
	DEY
	BPL DADD42
	BMI DNORM
;
DADD50	LDA FACLSD,X
	SBC ARGLSD,X
	STA FACLSD,X
	INX
	DEY
	BPL DADD50
;
;**DECIMAL NORMALIZE FAC WITH NO LSR**
;
DNORMZ	CLC
;
;**DECIMAL NORMALIZE FAC WITH POTENTIAL LSR**
;
DNORM	CLD
	BCS DNOR20
;
;BAIL OUT IF MANTISSA ZERO
;
	LDA #0
	LDX #FACMSD-FACLSD
DNORM2	ORA FACLSD,X
	DEX
	BPL DNORM2
	TAX
	BEQ DZEROF
;
;IS MSD SIGNIFICANT YET?
;
DNOR10	LDA FACMSD
	AND #$F0
	BNE DNOR40      ;YES...DONE
;
;SHIFT FAC LEFT ONE DIGIT
;
	LDY #3
DNOR12	LDX #0
	CLC
	PHP
DNOR14	PLP
	ROL FACLSD,X
	PHP
	INX
	CPX #1+FACMSD-FACLSD
	BCC DNOR14
	PLP
	DEY
	BPL DNOR12
;
;DECREMENT FACEXP WITH UNDERFLOW PROTECTION
;
	LDA FACEXP
	LSR A
	PHP
	SEC
	SBC #1
	CMP #$3F
	BNE DNOR15
	PLP
	JMP DZEROF      ;UNDERFLOW
DNOR15	PLP
	ROL A
	STA FACEXP
	JMP DNOR10
;
;SHIFT FAC RIGHT ONE DIGIT
;
DNOR20	LDY #3
DNOR22	LDX #FACMSD-FACLSD
	CLC
DNOR24	ROR FACLSD,X
	DEX
	BPL DNOR24
	DEY
	BPL DNOR22
;
;MAKE MSD A 1 FROM CARRY
;
	LDA FACMSD
	ORA #$10
	STA FACMSD
;
;INCREMENT FACEXP GUARD OVERFLOW
;
	LDA FACEXP
	LSR A
	PHP
	CLC
	ADC #1
	CMP #$40
	BEQ DOVERR      ;CASE $7F+$01->$80
	PLP
	ROL A
	STA FACEXP
;
DNOR40	RTS
;
DOVERR	JMP OVERR
;
;**PUT DECIMAL ZERO IN FAC**
;
DZEROF	LDA #0
	LDX #FACMSD-FACLSD
DZERO2	STA FACLSD,X
	DEX
	BPL DZERO2
	STA FACEXP
	RTS
.END
