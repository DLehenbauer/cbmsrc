.PAG 'CODE2'
;*****CODE2*****
BUFOFS	=@1000
CRUNCH	JMP (ICRNCH)    ;GO THROU INDIRECT
NCRNCH	LDX TXTPTR
	LDY #4
	STY DORES
;
	BCS KLOOP       ;DIRECT MODE SO CRUNCH
;
	LDA CRNCH       ;TEST IF CRUNCHER ON OR..
	BEQ STR1        ;...OFF
;
KLOOP	LDA BUFOFS,X
	BPL CMPSPC
	CMP #PI
	BEQ STUFFH
	INX
	BNE KLOOP
CMPSPC	CMP #' 
	BEQ STUFFH
;
	CMP #$27        ;TURN CRUNCHER OFF CHAR(SINGLE QUOTE ')
	BNE CMPCTN
	LDA #0          ;END ON NULL
	STA ENDCHR
	BEQ STR1        ;BRA
CMPCTN	;CONTINUE
;
	STA ENDCHR
	CMP #34
	BEQ STRNG
	BIT DORES
	BVS STUFFH
	CMP #'?
	BNE KLOOP1
	LDA #PRINTK
	BNE STUFFH
KLOOP1	CMP #'0
	BCC MUSTCR
	CMP #60
	BCC STUFFH
MUSTCR	STY BUFPTR
	LDY #0
	STY COUNT
	STX TXTPTR
	LDA #>RESLST
	STA INDEX1+1
	LDA #<RESLST
	STA INDEX1
	BNE RESCON      ;BRANCH ALWAYS
.SKI 3
RESER	INX
	INC INDEX1
	BNE RESCON
	INC INDEX1+1
RESCON	LDA BUFOFS,X
	SEC
	SBC (INDEX1)Y
	BEQ RESER
	CMP #128
	BNE NTHIS
	ORA COUNT
GETBPT	LDY BUFPTR
STUFFH	INX
	INY
	STA BUF-5,Y
	LDA BUF-5,Y
	BEQ CRDONE
	SEC
	SBC #':
	BEQ COLIS
	CMP #DATATK-$3A
	BNE NODATT
COLIS	STA DORES
NODATT	SEC
	SBC #REMTK-$3A
	BNE KLOOP
	STA ENDCHR
STR1	LDA BUFOFS,X
	BEQ STUFFH
	CMP ENDCHR
	BEQ STUFFH
STRNG	INY
	STA BUF-5,Y
	INX
	BNE STR1
NTHIS	LDX TXTPTR
	INC COUNT
.SKI 3
;LOOK FOR END OF RESERVED WORD
NTHIS1	LDA (INDEX1)Y
	PHP
	INC INDEX1
	BNE NTHIS2
	INC INDEX1+1
NTHIS2	PLP
	BPL NTHIS1
.SKI 3
;LOOK FOR END OF RESLST
	LDA (INDEX1)Y
	BNE RESCON
	LDA BUFOFS,X
	BPL GETBPT
.SKI 3
CRDONE	STA BUF-3,Y
	DEC TXTPTR+1
ZZ1	=BUF-1
	LDA #<ZZ1
	STA TXTPTR
	RTS
FNDLIN	LDA TXTTAB
	LDX TXTTAB+1
FNDLNC	LDY #1
	STA LOWTR
	STX LOWTR+1
	LDA (LOWTR)Y
	BEQ FLINRT
	INY
	INY
	LDA LINNUM+1
	CMP (LOWTR)Y
	BCC FLNRTS
	BEQ FNDLO1 
	DEY
	BNE AFFRTS
FNDLO1	LDA LINNUM
	DEY
	CMP (LOWTR)Y
	BCC FLNRTS
	BEQ FLNRTS
AFFRTS	DEY
	LDA (LOWTR)Y
	TAX
	DEY
	LDA (LOWTR)Y
	BCS FNDLNC
FLINRT	CLC
FLNRTS	RTS
SCRATH	BNE FLNRTS
SCRTCH	LDA #0
	TAY
	STA (TXTTAB)Y
	INY
	STA (TXTTAB)Y
	LDA TXTTAB
	CLC
	ADC #2
	STA VARTAB
	LDA TXTTAB+1
	ADC #0
	STA VARTAB+1
RUNC	JSR STXTPT
	LDA #0
	STA ACTERR      ;DEACTIVATE ON ERROR GOTO
CLEAR	BEQ CLEARC      ;IF HE SAID "CLR" ONLY BRANCH
	CMP #ERRTK      ;ELSE MAKE SURE HE SAID ERROR
	BNE CLRTS       ;ELSE RETURN
YHEDID	LDA #$80        ;SET BIT 7
	AND ACTERR      ;CLEAR BIT 6
	STA ACTERR      ;UPDATE ERROR STATUS
	JSR CHRGET      ;POINT TO NEXT BASIC STATEMENT
CLRTS	RTS             ;RETURN
CLEARC	LDA MEMSIZ
	LDY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	LDA #0
	STA DSDESC
	STA ACTERR      ;DISABLE ON ERROR
	STA CASS1       ;KILL CASS ALLOCATIONS
	STA CASS2
	JSR CLALL
	JSR PUDECL      ;RESTORE PRINT USING DEFINES
	LDA VARTAB
	LDY VARTAB+1
	STA ARYTAB
	STY ARYTAB+1
	STA STREND
	STY STREND+1
FLOAD	JSR RSTALL      ;NEW RESTORE
STKINI	LDX #TEMPST
	STX TEMPPT
	PLA 
	TAY
	PLA
	LDX #STKEND-257
	TXS
	PHA
	TYA
	PHA
	LDA #0
	STA OLDTXT+1
	STA SUBFLG
STKRTS	RTS
STXTPT	CLC
	LDA TXTTAB
	ADC #255
	STA TXTPTR
	LDA TXTTAB+1
	ADC #255
	STA TXTPTR+1
	RTS
.END
