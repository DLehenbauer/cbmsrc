.PAG 'INITIALIZATION CODE'
; RSR 7/16/80
INIT	LDX #STKEND-256
	TXS
	LDA #$4C
	STA JMPER
	STA USRPOK
	LDA #<FCERR
	LDY #>FCERR
	STA USRPOK+1
	STY USRPOK+2
	LDX #13         ;RND AND INDIRECT INITS
MOVCHG	LDA RNDINT-1,X
	STA RNDX-1,X
	DEX
	BNE MOVCHG
	LDA #STRSIZ
	STA FOUR6
	TXA
	STA BITS
	STA CHANNL
	STA LASTPT+1
	STA DSDESC
	PHA
	INX
	STX BUF-3
	STX BUF-4
	LDX #TEMPST
	STX TEMPPT
	LDY #>RAMLOC
	STA TXTTAB
	STY TXTTAB+1
	STA LINNUM
	STY LINNUM+1
	TAY
.SKI
;CREATE RAM INITIALIZATION IF NEEDED
	LDA #>OFFSET
	BNE LOOPMM
	LDA #<RAMEND
	LDY #>RAMEND
	JMP USEDEF
LOOPMM	INC LINNUM
	BNE LOOPM1
	INC LINNUM+1
	BMI USEDEC
LOOPM1	LDA #85
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BNE USEDEC
	ASL A
	STA (LINNUM)Y
	CMP (LINNUM)Y
	BEQ LOOPMM
USEDEC	LDA LINNUM
	LDY LINNUM+1
USEDEF	STA MEMSIZ
	STY MEMSIZ+1
	STA FRETOP
	STY FRETOP+1
	LDA PAROM       ;CHECK TO SEE IF PA ROM IN
	CMP #'P
	BNE NOXA
	LDA PAROM+1
	CMP #'A
	BNE NOXA
	LDA #<PAROMC    ;REDIRECT DISPATCH TO PA ROM CODE
	STA IGONE
	LDA #>PAROMC
	STA IGONE+1
NOXA	JSR PUDECL      ;SET UP PRINT USING DEFINES
	LDA #0
	LDY #LAT-USINGV ;INIT BASIC SYS NON-ZEROPAGE SPACE
INITBL	STA USINGV,Y
	DEY
	BPL INITBL
	STA CASS1       ;CLR CASS FOR USE
	STA CASS2       ;BOTH
	STY CRNCH       ;COME UP IN BASIC
	INY             ;RESTORE Y TO ZERO
	STA (TXTTAB)Y
	INC TXTTAB
	LDA #<FREMES
	LDY #>FREMES
	JSR STROUT
	LDA MEMSIZ
	SEC
	SBC TXTTAB
	TAX
	LDA MEMSIZ+1
	SBC TXTTAB+1
	JSR LINPRT
	LDA #<WORDS
	LDY #>WORDS
	JSR STROUT
	JSR SCRTCH
	JMP READY
;
PUDECL	LDA #'*         ;SET UP PRINT USING DEFINES
	STA FILLCH
	LDA #'$
	STA MONEY
	LDA #'.
	STA TENTHS
	LDA #',
	STA THOUSD
	RTS
;
.END
