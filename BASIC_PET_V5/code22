.PAG 'CODE22'
N0999	.BYT @233,@76,@274,@37,@375
N9999	.BYT @236,@156,@153,@47,@375
NMIL	.BYT @236,@156,@153,@50,0
.SKI 3
INPRT	LDA #<INTXT
	LDY #>INTXT
	JSR STROUT
;
; LNNPRT - PRINT CURRENT LINE NUMBER
;
LNNPRT	LDA CURLIN+1
	LDX CURLIN
;
; LINPRT - PRINT OUT 2-BYTE NUMBER 0-65535
;      .X - LOW BYTE
;      .A - HIGH BYTE
;
LINPRT	JSR PAFOT0      ;CONVERT TO ASCII IN BUFFER
STROU2	JMP STROUT      ;BUFFER TO OUTPUT DEVICE
;
; FOUBIN - FAC OUTPUT - BINARY TYPE INPUT
;   INPUTS - FAC = NUMBER TO PRINT
;   OUTPUTS- FBUFFR CONTAINS ASCII NUMBER
;
FOUBIN	LDY #1          ;OLD FOUT ROUTINE
FOUTC	LDA TARGET      ;SAVE GLOBAL TARGET
	PHA
	LDA #0
	STA TARGET      ;LOCAL TARGET IS BIN
	LDA #$20        ;SPACE FIX 10/16/79
	BIT FACSGN
	BPL FOUT1
	LDA #'-
FOUT1	STA FBUFFR-1,Y
	STA FACSGN
	STY FBUFPT
	INY
	LDA #'0
	LDX FACEXP
	BNE *+5
	JMP FOUT19
	JSR FOUT1X      ;RSR CHOP 11/21/79
	JMP BIGG00      ;
FOUT1X	LDA #0          ;
	CPX #@200
	BEQ FOUT37
	BCS FOUT7
FOUT37	LDA #<NMIL
	LDY #>NMIL
	JSR FMULT
	LDA #250-ADDPR2-ADDPRC
FOUT7	STA DECCNT
FOUT4	LDA #<N9999
	LDY #>N9999
	JSR FCOMP
	BEQ BIGGES
	BPL FOUT9
FOUT3	LDA #<N0999
	LDY #>N0999
	JSR FCOMP
	BEQ FOUT38
	BPL FOUT5
FOUT38	JSR MUL10
	DEC DECCNT
	BNE FOUT3
FOUT9	JSR DIV10
	INC DECCNT
	BNE FOUT4
FOUT5	LDA TARGET      ;SAVE GLOBAL TARGET
	PHA
	LDA #0          ;LOCAL TARGET IS BIN
	STA TARGET
	JSR FADDH
	PLA             ;RESTORE GLOBAL TARGET
	STA TARGET
BIGGES	JSR QINT
	LDX #1
	LDA DECCNT
	CLC
	ADC #ADDPR2+ADDPRC+7 ;RSR CHOP 11/21/79
	RTS
;
BIGG00	BMI FOUTPI
	CMP #ADDPR2+ADDPRC+@10
	BCS FOUT6
	ADC #$FF
	TAX
	LDA #2
FOUTPI	SEC
FOUT6	SBC #2
	STA TENEXP
	STX DECCNT
	TXA
	BEQ FOUT39
	BPL FOUT8
FOUT39	LDY FBUFPT
	LDA #'.
	INY
	STA FBUFFR-1,Y
	TXA
	BEQ FOUT16
	LDA #'0
	INY
	STA FBUFFR-1,Y
FOUT16	STY FBUFPT
FOUT8	LDY #0
FOUTIM	LDX #@200
FOUT2	JSR FOUT2X
	JMP FOUT43
FOXTIM	LDA #$FF        ;SAVE TARGET STRING
	PHA
	BNE FOUTIM      ;JMP
;
FOUT2X	LDA FACLO       ;RSR CHOP 11/21/79
	CLC
	ADC FOUTBL+2+ADDPRC,Y
	STA FACLO
	LDA FACMO
	ADC FOUTBL+1+ADDPRC,Y
	STA FACMO
	LDA FACMOH
	ADC FOUTBL+1,Y
	STA FACMOH
	LDA FACHO
	ADC FOUTBL,Y
	STA FACHO
	INX
	BCS FOUT41
	BPL FOUT2X
	BMI FOUT40
FOUT41	BMI FOUT2X
FOUT40	TXA
	BCC FOUTYP
	EOR #@377
	ADC #@12
FOUTYP	ADC #@57
	INY
	INY
	INY
	INY
	STY FDECPT
	LDY FBUFPT
	INY
	TAX
	AND #@177
	RTS
;
FOUT43	STA FBUFFR-1,Y  ;RSR CHOP 11/21/79
	DEC DECCNT
	BNE STXBUF
	LDA #'.
	INY
	STA FBUFFR-1,Y
STXBUF	JSR FOUT3X      ;RSR CHOP 11/21/79
	JMP FOUT44
;
FOUT3X	STY FBUFPT
	LDY FDECPT
	TXA
	EOR #@377
	AND #@200
	TAX
	CPY #FDCEND-FOUTBL
	RTS
;
FOUT44	BEQ FOULDY
	CPY #TIMEND-FOUTBL
	BNE FOUT2
FOULDY	LDY FBUFPT
FOUT11	LDA FBUFFR-1,Y
	DEY
	CMP #'0
	BEQ FOUT11
	CMP #'.
	BEQ FOUT12
	INY
FOUT12	LDA #'+
	LDX TENEXP
	BEQ FOUT17
	BPL FOUT14
	LDA #0
	SEC
	SBC TENEXP
	TAX
.END
