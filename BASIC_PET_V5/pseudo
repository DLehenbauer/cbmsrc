.PAGE 'PSEUDO DIV&MULT'
;
; PESUDO DIVIDE & MULTIPLY SUBROUTINE
; FOR LOG AND EXP CALCULATIONS
; RSR 4-14-80
;
; USES -FAC,ARG,RES,FBUFFR- REGISTERS
; VARIABLES -FDIVP- 0-LOG 1-EXP
;           -CDIVP- COUNT TEMP
;           -JDIVP- COUNT TEMP
;           -TDIVP- LOOP COUNT TEMP
;
PSDIV	LDA #0          ;CLEAR REGISTERS
	LDX #ARGMSD-ARGLSD
PSDX00	STA RESLSD,X
	STA ARGLSD,X
	DEX
	BPL PSDX00
	TAX             ;J=0
	STX FACMSD+1    ;WATCH DIGITS=0
	STX ARGMSD+1
	LDA #$10        ;ARG=1.000000ETC.
	STA ARGMSD
;
PSD000	STX JDIVP       ; J=.X
	LDA #0
	STA CDIVP       ;C=0
	LDA FDIVP       ;IF LOG
	BEQ PSD010      ; THEN A=A-B
;
	LDY LN12,X      ; ELSE B=L(J)
	LDX #ARGMSD-ARGLSD
PSD002	LDA LNXX0,Y     ;TRANSFER FROM TABLES TO ARG
	STA ARGLSD,X
	DEY
	DEX
	BPL PSD002
;
	LDY #3          ;ARG/10
PSD004	CLC
	LDX #ARGMSD-ARGLSD
PSD006	ROR ARGLSD,X
	DEX
	BPL PSD006
	DEY
	BPL PSD004
;
PSD010	LDX #0          ;FAC=FAC-ARG
	LDY #FACMSD-FACLSD+1
	SEC
	SED
PSD020	LDA FACLSD,X
	SBC ARGLSD,X
	STA FACLSD,X
	INX
	DEY
	BPL PSD020
	CLD
;
	INC CDIVP       ;C-C+1
	BCC PSD030      ; BORROW
;
	LDA FDIVP
	BNE PSD010      ;IF EXP THEN LOOP AGAIN
	JSR PSDP10      ;B=B+B*10^(-J-1)
	JMP PSD010
;
PSD030	DEC CDIVP       ;C=C-1 ADJUST
	LDX #0          ;FAC=FAC+ARG
	LDY #FACMSD-FACLSD+1
	CLC
	SED
PSD040	LDA FACLSD,X
	ADC ARGLSD,X
	STA FACLSD,X
	INX
	DEY
	BPL PSD040
	CLD
;
	LDA #3          ;FAC=FAC*10
	STA TDIVP
PSD042	LDX #0
	LDY #FACMSD-FACLSD+1
	CLC
PSD044	ROL FACLSD,X
	INX
	DEY
	BPL PSD044
	DEC TDIVP
	BPL PSD042
;
PSD050	LDA JDIVP       ;RES=RES*10 ..BACKWARDS INDEX..
	LSR A           ;RES=RES+C
	TAX             ;ASSUMED 0=<C<10
	LDA CDIVP
	BCC PSD060
	ASL A
	ASL A
	ASL A
	ASL A
PSD060	ORA RESLSD,X
	STA RESLSD,X
	LDX JDIVP
	INX
	CPX #22         ;NUMBER OF DIGITS
	BEQ PSD070      ;DONE
	JMP PSD000
;
; 2ND HALF
;
PSD070	LDA #0          ;CLEAR REGISTERS
	LDX #FACMSD-FACLSD+1
PSD072	STA FACLSD,X
	STA ARGLSD,X
	DEX
	BPL PSD072
	TAX
	LDA #$10        ;ARG=1
	STA ARGMSD
	CPX FDIVP       ; .X =0
	BNE PSD080      ;EXP
	LDX #21         ;LOG N-1
PSD080	STX JDIVP
	LDA FDIVP
	BNE PSD090      ;EXP
	LDY LN12,X
	LDX #ARGMSD-ARGLSD
PSD085	LDA LNXX0,Y     ;TRANSFER LOG(J) INTO ARG
	STA ARGLSD,X
	DEY
	DEX
	BPL PSD085
;
PSD090	LDA JDIVP       ;..BACKWARDS INDEX..
	LSR A           ;C=(LOG=LSD OR EXP=MSD OF RES)
	TAX
	LDA RESLSD,X
	BCC PSD100
	LSR A
	LSR A
	LSR A
	LSR A
PSD100	AND #$0F
	STA CDIVP
;
PSD110	DEC CDIVP       ;C=C-1
	BMI PSD180      ; IF C<0 THEN ADJUST FAC
;
	LDX #0          ; ELSE FAC=FAC+ARG
	LDY #FACMSD-FACLSD+1
	CLC
	SED
PSD120	LDA FACLSD,X
	ADC ARGLSD,X
	STA FACLSD,X
	INX
	DEY
	BPL PSD120
	CLD
	LDA FDIVP       ;IF LOG THEN JUST LOOP
	BEQ PSD110
;
	JSR PSDP10
	JMP PSD110
;
PSD180	LDA FDIVP
	BEQ PSD210      ;LOG
;
	LDY #3          ;ARG=ARG/10
PSD190	LDX #ARGMSD-ARGLSD
	CLC
PSD200	ROR ARGLSD,X
	DEX
	BPL PSD200
	DEY
	BPL PSD190
;
	LDX JDIVP       ;J=J+1
	INX
	CPX #22; IF J=N
	BEQ PSD240      ; THEN QUIT
PSD205	JMP PSD080      ; ELSE AGAIN
;
PSD210	LDY #3          ;FAC=FAC/10
PSD220	CLC
	LDX #FACMSD-FACLSD+1
PSD230	ROR FACLSD,X
	DEX
	BPL PSD230
	DEY
	BPL PSD220
;
	LDX JDIVP       ;J=J-1
	DEX
	BPL PSD205      ;IF J<>0 THEN AGAIN
PSD240	RTS             ;ELSE STOP
;
;
PSDP10	LDX #ARGMSD-ARGLSD ;B*10^(-J)
PSD132	LDA ARGLSD,X
	STA FBUFFR,X
	DEX
	BPL PSD132
;
	LDA JDIVP       ;CALC OFFSET INTO RES
	LSR A
	BCC PSD160      ;NON-ODD POWERS OF 10^(-J)
	LDY #3
PSD140	CLC             ; TEMP=TEMP/10
	LDX #FACMSD-FACLSD
PSD150	ROR FBUFFR,X
	DEX
	BPL PSD150
	DEY
	BPL PSD140
;
PSD160	LDA JDIVP       ;ARG=ARG+ARG*10^(-J)
	LSR A
	TAY
	CLC
	ADC #$F5        ;0-12+J
	BEQ PSD172      ;LAST DIGIT LOOPS SO QUIT
	STA TDIVP       ;INTO TEMP
	LDX #0
	SED
PSD170	LDA FBUFFR,Y
PSD171	ADC ARGLSD,X
	STA ARGLSD,X
	INX
	INY
	INC TDIVP
	BMI PSD170
	LDA #0
	BCS PSD171
PSD172	CLD
	RTS
;
.END
