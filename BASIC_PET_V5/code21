.PAG 'CODE21'
QINT	LDA FACEXP
	BEQ CLRFAC
	SEC
	SBC #ADDPR8+@230
	BIT FACSGN
	BPL QISHFT
	TAX
	LDA #@377
	STA BITS
	JSR NEGFCH
	TXA
QISHFT	LDX #FAC
	CMP #$F9
	BPL QINT1
	JSR SHIFTR
	STY BITS
QINTRT	RTS
QINT1	TAY
	LDA FACSGN
	AND #@200
	LSR FACHO
	ORA FACHO
	STA FACHO
	JSR ROLSHF
	STY BITS
INTR	RTS
INT	LDA FACTYP
	BEQ INTB        ;BINARY
	LDA FACEXP      ;DECIMAL TRUNCATE
	BPL *+5         ;MUST BE >0
	JMP DZEROF      ;OR ZERO ALL
	LSR A           ;DUMP MANTISSA SIGN
	CMP #21         ;ALL INT ?
	BCS INTR        ;YES..DONE
	EOR #$FF
	ADC #22         ;21-A GIVES INDEX
TRUNCD	LSR A
	TAX
	LDA FACLSD,X
	BCC TRUNC0      ;NOT UPPER
	AND #$F0        ;SAVE UPPER
	STA FACLSD,X
TRUNC0	JMP BR03        ;USE BCDRND TO CHOP REST
;
INTB	LDA FACEXP
	CMP #ADDPR8+@230
	BCS INTRTS
	JSR QINT
	STY FACOV
	LDA FACSGN
	STY FACSGN
	EOR #@200
	ROL A
	LDA #@230+8
	STA FACEXP
	LDA FACLO
	STA INTEGR
	JMP FADFLT
CLRFAC	STA FACHO
	STA FACMOH
	STA FACMO
	STA FACLO
	TAY 
INTRTS	RTS
FINBIN	LDY #@0
	STY TARGET      ;CLRS UP INT CONFLICTS
	LDX #$11        ;CLR FAC & FLAGS UP TO SGNFLG
FINZLP	STY DECCNT,X
	DEX
	BPL FINZLP
	BCC FINDGQ
	JSR SIGNCK
	BNE FIN1
	BCC FINC
	STX SGNFLG
FINC	JSR CHRGET
FINDGQ	BCC FINDIG
FIN1	CMP #'.
	BEQ FINDP
	CMP #'E
	BNE FINE
	JSR CHRGET
	BCC FNEDG1
	JSR SIGNCK
	BNE FINEC2
FINEC1	ROR EXPSGN
FINEC	JSR CHRGET
FNEDG1	BCC FINEDG
FINEC2	BIT EXPSGN
	BPL FINE
	LDA #0
	SEC
	SBC TENEXP
	JMP FINE1
FINDP	ROR DPTFLG
	BIT DPTFLG
	BVC FINC
FINE	LDA TENEXP
FINE1	SEC
	SBC DECCNT
	STA TENEXP
	BEQ FINQNG
	BPL FINMUL
FINDIV	JSR DIV10
	INC TENEXP
	BNE FINDIV
	BEQ FINQNG
FINMUL	JSR MUL10
	DEC TENEXP
	BNE FINMUL
FINQNG	LDA SGNFLG
	BMI NEGXQS
	RTS
NEGXQS	JMP NEGOP
FINDIG	PHA
	BIT DPTFLG
	BPL FINDG1
	INC DECCNT
FINDG1	JSR MUL10
	PLA
	SEC
	SBC #'0
	JSR FINLOG
	JMP FINC
FINLOG	PHA
	JSR MOVAF
	PLA
	JSR FLOAT
	LDA ARGSGN
	EOR FACSGN
	STA ARISGN
	LDA TARGET      ;SAVE GLOBAL TARGET
	PHA
	LDA #0          ;LOCAL TARGET IS BIN
	STA TARGET
	JSR FADDT
	PLA             ;RESTORE GLOBAL TARGET
	STA TARGET
	RTS
FINEDG	LDA TENEXP
	CMP #@12
	BCC MLEX10
	LDA #@144
	BIT EXPSGN
	BMI MLEXMI
	JMP OVERR
MLEX10	ASL A
	ASL A
	CLC
	ADC TENEXP
	ASL A
	CLC
	LDY #0
	ADC (TXTPTR)Y
	SEC
	SBC #'0
MLEXMI	STA TENEXP
	JMP FINEC
.END
