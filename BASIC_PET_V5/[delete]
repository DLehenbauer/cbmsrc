.PAG 'DELETE'
;**********************************
;
; DELETE FROM#-TO#
;
;**********************************
DELETX	BNE DEL100      ;NOTHING IS ERROR
	JMP SNERR
DEL100	JSR RANGE       ;GET LINE # RANGE
	JSR PRTRTN      ;CHK SYNTAX
	LDA LOWTR       ;SAVE IT
	LDX LOWTR+1
	STA INDEX2
	STX INDEX2+1
	JSR FNDLIN      ;FIND IT
	LDA LOWTR       ;LINE ADR FOUND
	LDX LOWTR+1
	BCC DEL300      ;SKIP IF NOT FOUND
	LDY #1
	LDA (LOWTR),Y   ;AT END OF SOURCE?
	BEQ DEL300      ;YES
	TAX
	DEY
	LDA (LOWTR),Y
DEL300	STA TXTPTR      ;INCLUDE TO LINE #
	STX TXTPTR+1
	LDA INDEX2      ;CHECK FROM# < TO#
	SEC
	SBC TXTPTR      ;GEN NEG DELTA
	TAX
	LDA INDEX2+1
	SBC TXTPTR+1
	TAY
	BCS NOTDEL      ;NO GOOD
	TXA
	CLC
	ADC VARTAB      ;GEN NEW END OF SOURCE
	STA VARTAB
	TYA
	ADC VARTAB+1
	STA VARTAB+1
	LDY #0
DEL500	LDA (TXTPTR),Y  ;MOVE SOURCE DOWN
	STA (INDEX2),Y
	INY
	BNE DEL500
	INC TXTPTR+1
	INC INDEX2+1
	LDA VARTAB+1    ;DONE ONE EXTRA PAGE
	CMP INDEX2+1
	BCS DEL500      ;NO
NOTDEL	JSR LNKPRG      ;FIX LINKS
	LDA INDEX1      ;CALC NEW END POINTER
	LDX INDEX1+1
	CLC
	ADC #2
	STA VARTAB
	BCC NOT500
	INX
NOT500	STX VARTAB+1
	JSR RUNC        ;FIX BASIC POINTERS
	JMP READY       ;SAY READY
.END
