.PAG 'DECIMAL COMPARE'
;DECIMAL COMPARE ARG:FAC
;DECEMBER 31,1979
;
;.A= 1,C=0 IF ARG .LT. FAC
;.A= 0,C=1 IF ARG .EQ. FAC
;.A=-1,C=1 IF ARG .GT. FAC
;
DCOMP	LDA FACEXP
	EOR ARGEXP
;
;ARE MANTISSA SIGNS SAME?
;
	LSR A
	BCS DCOM10      ;NO...
;
;ARE EXPONENT SIGNS SAME?
;
	ROL A
	BMI DCOM20      ;NO...
;
;ARE EXPONENT MAGNITUDES SAME?
;
	BNE DCOM30      ;NO...
;
;COMPARE MANTISSA MAGNITUDES
;
	LDX #FACMSD-FACLSD-1
	SED
DCOM5	LDA ARGLSD+1,X
	CMP FACLSD+1,X
	BCC DCOM7
	BNE DCOM7
	DEX
	BNE DCOM5
DCOM7	CLD
	BNE DCOM40
	TXA
	BEQ DCOM45
;
;CASE DIFFERENT MANTISSA SIGNS
;
DCOM10	LDA FACEXP
	ROR A
	JMP DCOM42
;
;CASE DIFFERENT EXPONENT SIGNS
;
DCOM20	SEC
	LDA FACMSD      ;TEST FOR ZEROS
	BEQ DCOM42      ;ARG .GT. FAC
	LDA ARGMSD
	BEQ DCOM44      ;ARG .LT. FAC
	LDA FACEXP
	ROL A
	JMP DCOM40
;
;CASE DIFFERENT EXPONENT MAGNITUDES
;
DCOM30	SEC
	LDA ARGEXP
	SBC FACEXP
;
;HANDLE NEGATIVE MANTISSA
;
DCOM40	ROL A
	EOR ARGEXP
	LSR A
;
;COMMON EXIT CODE
;
DCOM42	LDA #$FF
	BCS DCOM45
DCOM44	LDA #$01
;
DCOM45	RTS
.END
