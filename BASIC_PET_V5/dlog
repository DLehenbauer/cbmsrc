.PAG 'DECIMAL LOG'
;
; DECIMAL LOG FAC=LOG(FAC)
; RJF 2-29-80  RSR 4-14-80
;
; IF MANTISSA NEGATIVE OR ZERO
; THEN END WITH ILLEGAL QUANTITY ERROR
;
DLOG	LDA FACEXP
	LSR A           ;CHECK SIGN
	BCS DLN010      ;IS NEGATIVE
	LDX FACMSD
	BNE DLN020      ;IS NON-ZERO
DLN010	JMP FCERR       ;ILLEGAL QUANTITY
;
; SAVE EXPONENT FOR PART2 OF LOG
;
DLN020	PHA
;
; SUBTRACT 1 FROM MSD OF ARGUMENT
;
	SEC
	LDA FACMSD
	SBC #$10
	STA FACMSD
;
; SET FUNCTION LOG AND DO PSEUDO
; DIVIDE-PSEUDO MULTIPLY.  PSEUDO PRODUCT
; RETURNED IN FAC.
;
	LDA #0
	STA FUNC
	JSR PSDIV
;
; CLEAR EXPONENT OF FAC
; AND NORMALIZE PSEUDO PRODUCT
;
	LDA #0
	STA FACEXP
	CLC
	JSR DNORM
;
; SAVE FAC WHILE PART 2 IS PERFORMED
; CLEAR FAC AS WE GO
;
	LDY #0
	LDX #FACMSD-FACLSD+1 ;AND FACEXP
DLN030	LDA FACEXP,X
	STA FBUFFR,X
	STY FACEXP,X
	DEX
	BPL DLN030
;
; CONVERT EXPONENT TO BCD AND PLACE IN FAC
;
	PLA
	PHA
	AND #$40
	BEQ DLN040
	PLA
	EOR #$7F
	CLC
	ADC #1
	PHA
	INC FACEXP      ;NEGATE MANTISSA
;
DLN040	PLA
DLN050	JSR BINDEC      ;CONVERT TO DECIMAL
DLN060	PHA
	INC FACEXP      ;ASSUME >10
	INC FACEXP
	AND #$F0
	BNE DLN070
	PLA             ;NORMALIZE FAC
	ASL A
	ASL A
	ASL A
	ASL A
	PHA
	DEC FACEXP      ;ASSUME <10
	DEC FACEXP
DLN070	PLA
	STA FACMSD
	BEQ DLN082
;
; MULTIPY EXPONENT BY LOG 10
;
	LDX #ARGMSD-ARGLSD
DLN080	LDA LN10,X
	STA ARGLSD,X
	DEX
	BPL DLN080
	LDA #0
	STA ARGEXP
	JSR DMULT
;
; RESTORE PSEUDO PRODUCT AND ADD
;
DLN082	LDX #ARGMSD-ARGLSD+1 ;ALSO GET EXP
DLN090	LDA FBUFFR,X
	STA ARGEXP,X
	DEX
	BPL DLN090
	JMP DADD
;
.END
