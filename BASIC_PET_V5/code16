.PAG 'CODE16'
	LDA (STRNG1)Y
	TAX
	INY
	LDA (STRNG1)Y
	TAY
	PLA
MOVSTR	STX INDEX
	STY INDEX+1
MOVDO	TAY
	BEQ MVDONE
	PHA
MOVLP	DEY
	LDA (INDEX)Y
	STA (FRESPC)Y
	TYA
	BNE MOVLP
	PLA
MVDONE	CLC
	ADC FRESPC
	STA FRESPC
	BCC MVSTRT
	INC FRESPC+1
MVSTRT	RTS
FRESTR	JSR CHKSTR
FREFAC	LDA FACMO
	LDY FACMO+1
FRETMP	STA INDEX
	STY INDEX+1
	JSR FRETMS      ;CHECK DESC. IF LAST
	BNE FRE02       ;ONE THEN SCRATCH IT
	JSR STRADJ      ;INDEX POINTS TO LINK
	BCC FRE02       ;LITERAL NO FIX
	DEY             ;.Y=1
	LDA #$FF        ;FLAG STRING AS GARBAGE
	STA (INDEX),Y
	DEY
	TXA
	STA (INDEX),Y   ;PUT IN LENGTH
	PHA             ;SAVE LENGTH ON STACK
;
	EOR #255        ;PUT INDEX BACK
	SEC             ;TO FIRST BYTE
	ADC INDEX
	LDY INDEX+1
	BCS RES00
	DEY
RES00	STA INDEX
	STY INDEX+1
	TAX             ;LO INTO X
	PLA             ;PULL LENGTH FROM STACK
	CPY FRETOP+1    ;TEST FOR EQ TO FRETOP
	BNE FRERTS
	CPX FRETOP
	BNE FRERTS
;
;STRING WAS LAST INTO STRING SPACE
;SAVE GARBAGE COLLECTION SOME TIME
;BY FREEING UP. (LENGTH + 2)
;
	PHA             ;SAVE LENGTH ON STACK
	SEC             ;PLUS ONE
	ADC FRETOP
	STA FRETOP
	BCC FRE01
	INC FRETOP+1
FRE01	INC FRETOP      ;+ ONE MORE
	BNE FREPLA
	INC FRETOP+1
FREPLA	PLA             ;PULL LENGTH OFF STACK
	RTS
FRE02	LDY #$00        ;SET UP X,Y,A AND INDEX
	LDA (INDEX),Y   ;LENGTH
	PHA             ;ON STACK
	INY
	LDA (INDEX),Y   ;PIONTER LO
	TAX
	INY
	LDA (INDEX),Y   ;POINTER HI
	TAY
	STX INDEX
	STY INDEX+1
	PLA             ;GET BACK LENGTH
	RTS
FRETMS	CPY LASTPT+1
	BNE FRERTS
	CMP LASTPT
	BNE FRERTS
	STA TEMPPT
	SBC #STRSIZ
	STA LASTPT
	LDY #0
FRERTS	RTS
CHRD	JSR CONINT
	TXA
	PHA
	LDA #1
	JSR STRSPA
	PLA
	LDY #0
	STA (DSCTMP+1)Y
	PLA
	PLA
	PLA             ;REMOVE TYPE
	JMP PUTNEW
LEFTD	JSR PREAM
	CMP (DSCPNT)Y
	TYA
RLEFT	BCC RLEFT1
	LDA (DSCPNT)Y
	TAX
	TYA
RLEFT1	PHA
RLEFT2	TXA
RLEFT3	PHA
	JSR STRSPA
	LDA DSCPNT
	LDY DSCPNT+1
	JSR FRETMP
	PLA
	TAY
	PLA
	CLC
	ADC INDEX
	STA INDEX
	BCC PULMOR
	INC INDEX+1
PULMOR	TYA
	JSR MOVDO
	JMP PUTNEW
RIGHTD	JSR PREAM
	CLC
	SBC (DSCPNT)Y
	EOR #255
	JMP RLEFT
MIDD	LDA #255
	STA FACLO
	JSR CHRGOT
	CMP #41
	BEQ MID2
	JSR CHKCOM
	JSR GETBYT
MID2	JSR PREAM
	BEQ GOFUC
	DEX
	TXA
	PHA
	CLC
	LDX #0
	SBC (DSCPNT)Y
	BCS RLEFT2
	EOR #255
	CMP FACLO
	BCC RLEFT3
	LDA FACLO
	BCS RLEFT3
PREAM	JSR CHKCLS
	PLA
	TAY
	PLA
	STA JMPER+1
	PLA             ;PULL OLD RTS ADDRESS
	PLA
	PLA             ;PULL TARGET OFF (DIFF RETURN)
	PLA
	TAX
	PLA
	STA DSCPNT
	PLA
	STA DSCPNT+1
	LDA JMPER+1
	PHA
	TYA
	PHA
	LDY #0
	TXA
	RTS
LEN	JSR LEN1
	JMP SNGFLT
LEN1	JSR FRESTR
	LDX #0
	STX VALTYP
	TAY
	RTS
ASC	JSR LEN1
	BEQ GOFUC
	LDY #0
	LDA (INDEX1)Y
	TAY
	JMP SNGFLT
GOFUC	JMP FCERR
GTBYTC	JSR CHRGET
.END
