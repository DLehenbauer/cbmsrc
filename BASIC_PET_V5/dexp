.PAG 'DECIMAL EXPONENT'
; **DECIMAL EXPONENTIAL FUNCTION FAC=EXP(FAC)
; RJF 2-29-80  RSR 4-14-80
;
; ZERO CASE CHECK
;
DEXP	LDA FACMSD
	BNE DXP000
	CLC             ;NOT NEGATIVE
	PHP
	PHA             ;RESULTING EXP =0
	JMP DINV
;
; MULTIPLY ARGUMENT BY INVERSE LOG(10)
;
DXP000	LDX #ARGMSD-ARGLSD
DXP010	LDA ILN10,X
	STA ARGLSD,X
	DEX
	BPL DXP010
	LDA #$FE
	STA ARGEXP
	JSR DMULT
;
	LDA #0          ;CLR EXP BYTE
	STA FACMSD+1
;
; CHECK EXPONENT RANGE
;
	LDA FACEXP
	ROR A           ;SIGN INTO CARRY
	PHP             ;SAVE CARRY
	ASL A           ;DUMP NEGATIVE
	STA FACEXP      ;POS FACEXP
	BMI DXP055      ;NEGATIVE EXPONENT
	CMP #4
	BCC DXP020      ;ZERO OR ONE
;
DXP0FE	JMP FCERR       ;ILLEGAL QUANTITY
;
; SHIFT ARGUMENT LEFT
;
DXP020	LDA #3
	STA TDIVP
DXP025	LDX #0
	LDY #FACMSD-FACLSD+1
	CLC
DXP030	ROL FACLSD,X
	INX
	DEY
	BPL DXP030
	DEC TDIVP
	BPL DXP025
	LSR FACEXP      ;UNTILL ALL >=1 IN FACMSD+1
	BNE DXP020
;
; EXTRACT MSD OF EXPONENT OF RESULT
;
DXP040	LDX #0
	LDA FACMSD+1
	SED
	SEC
DXP050	BEQ DXP070
	INX
	SBC #$01
	BCS DXP050      ;BRANCH ALWAYS
;
; SET RESULT EXPONENT FOR NUMBERS <LN(10)
;
DXP055	LDA #0
	PHA
	BEQ DXP072      ;BRA
;
; MULTIPLY FAC BY LOG(10)
;
DXP070	CLD
	TXA
	ASL A           ;SHIFT TO TEST OVERFLOW
	BMI DXP0FE      ;POS OVERFLOW...ILLEGAL QUANITY ERROR
	PHA
	LDA FACMSD      ;CHK TO SEE IF WE NEED NORMALIZE
	AND #$F0
	BNE DXP072
	CLC
	JSR DNORM
DXP072	LDX #ARGMSD-ARGLSD ;NEGATIVE ENTRY
DXP075	LDA LN10,X
	STA ARGLSD,X
	DEX
	BPL DXP075
	LDA #0
	STA ARGEXP
	JSR DMULT
;
	LDA FACMSD+1    ;IF NO EXP
	BEQ DXP060      ; THEN NEGATIVE ALIAGN
	DEC FACEXP      ;ALIAGN POS CASES
	DEC FACEXP
;
; NEGATIVE ALIGN FAC
;
DXP060	LDA FACEXP      ;IF EXP IS ZERO
	BEQ DXP080      ;THEN NO ADJUST
DXP061	LDY #3
DXP062	LDX #FACMSD-FACLSD
	CLC
DXP065	ROR FACLSD,X
	DEX
	BPL DXP065
	DEY
	BPL DXP062
	INC FACEXP
	INC FACEXP
	BMI DXP061
;
; PERFORM PSEUDO DIVIDE/MULTIPLY
;
DXP080	LDA #1
	STA FUNC
	JSR PSDIV
;
; DECIMAL INVERSE FUNCTION (FAC)
;
DINV	LDX #ARGMSD-ARGLSD
	LDA #0
DINV10	STA ARGLSD,X
	DEX
	BPL DINV10
	STA ARGEXP
	LDA #$10
	STA ARGMSD
	SED
	CLC             ;NEED TO ADD 1 TO ADJUST RESULT
	ADC FACMSD
	STA FACMSD
	CLD
	PLA             ; COMPUTE FAC FOR RESULT
	STA FACEXP
;
	BCC *+5         ;DON'T WASTE TIME
	JSR DNORM       ;ELSE NORMALIZE
	PLP             ;CHECK TO SEE IF NEED TO INVERT
	BCC *+5
	JMP DDIV        ;YES...INVERT
	RTS             ;NO...DONE
.END
