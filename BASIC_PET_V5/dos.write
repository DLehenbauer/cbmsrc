.PAGE 'DOS SYNTAX & WRITE'
;********************************
;*
;*  PET2001 DISK ROUTINES
;*  --RSR--7/24/79--MOD 6/16/80
;*
;********************************
;
; TABLD SYMBOLS
XFN1	=$F1            ;SENDS FILENAME1
XFN2	=$F2            ;SENDS FILENAME2
XID	=$D0            ;SENDS DISK ID
XREC	=$E0            ;SENDS S FOR SEQ OR LRECL
XWRT	=$E1            ;SENDS W OR L
XD1	=$D1            ;SENDS DRIVE1
XD2	=$D2            ;SENDS DRIVE2
;
; TABLD
;
TABLD	; USED TO BUILD DISK STRINGS
; CATALOG
	.BYT $FF,'$',XD1,':',XFN1
; DOPEN  DSAVE DLOAD
	.BYT XD1,':',XFN1,',',XWRT,',',XREC
; APPEND
	.BYT XD1,':',XFN1,',','A'
; HEADER
	.BYT 'N',XD1,':',XFN1,',',XID
; COLLECT
	.BYT 'V',XD1
; BACKUP
	.BYT 'D',XD2,'=',XD1
; COPY
	.BYT 'C',XD2,':',XFN2,'=',XD1,':',XFN1
; CONCAT
	.BYT 'C',XD2,':',XFN2,'=',XD2,':',XFN2
	.BYT ',',XD1,':',XFN1
; RENAME
	.BYT 'R',XD1,':',XFN2,'=',XD1,':',XFN1
; SCRATCH
	.BYT 'S',XD1,':',XFN1
;
.PAGE 'DOS SYNTAX & WRITE'
;
; CATALOG  (BOB F'S)
;
CATLOZ	LDA LA          ;SAVE LA
	STA WSW
	JSR DOSPAR      ;PARSE THE LINE
	AND #$E6        ;CHK SYNTAX
	BEQ CATALG
	JMP SNERR
CATALG	LDY #0          ;TABLD OFFSET
	LDX #1          ;JUST $
	LDA PARCHK
	AND #$11        ;NO DRIVE?
	BEQ CATBLD
	LSR A           ;CHK FOR FILENAME
	BCC CATNFN
	INX
	INX
CATNFN	INX
CATBLD	TXA             ;A NOW HAS LENGTH
	JSR SENDP       ;BUILD
	LDA DFLTO       ;SAVE DFLTO
	STA CNTDN
	LDA #$60        ;SECONDARY ADDR
	STA SA
	LDA #14         ;OPEN THE FILE
	STA LA
	JSR UNLSN       ;DON'T LISTEN TO FLOPPY
	JSR FOPEN
	LDA #0
	STA SATUS       ;SET STATUS TO 0
	LDY #$03        ;LOOP THREE TIMES
;
WG220	STY FNLEN       ;SAVE NEW COUNT
	JSR SUBC        ;DISK CHANNEL
	STA TMP2
	LDY SATUS       ;CHECK STATUS
	BNE WG230       ;BAD STATUS
	JSR BASIN
	STA TMP2+1
	LDY SATUS       ;CHECK STATUS
	BNE WG230
	LDY FNLEN       ;MORE TO DO?
	DEY
	BNE WG220       ;NOT DONE YET
	JSR CLRCHN      ;CLEAR CHANNEL
	JSR SUBB
	LDX TMP2
	LDA TMP2+1
	JSR LINPRT      ;PRINT LINE NUMBER
	LDA #$20        ;PRINT A SPACE
SKIPB	JSR SUBD
WG250	JSR SUBC        ;DISK CHANNEL
	PHA
	JSR CLRCHN
	PLA
	LDX SATUS
	BNE WG230       ;BAD
	CMP #0          ;EOL
	BEQ WG240
	JSR SUBA
	JSR STOP1       ;STOP KEY
	BEQ WG230       ;YES...
	JSR GETIN       ;GET CHAR FROM KBYD
	BEQ WG250       ;NOTHING...
	CMP #$20        ;SPACE BAR?
	BNE WG250       ;NO...
WG255	JSR GETIN       ;ANY KEY STARTS
	BEQ WG255
	BNE WG250       ;(JMP)
WG240	LDA #CR
	JSR SUBA
	JSR UNLSN
	LDY #$02        ; DO TWICE
	BNE WG220
WG235	PLA             ;CLEAN UP
WG230	JSR CLRCHN
	LDA #14         ;CLOSE FLOPPY
	JMP FCLOSE
;
SUBA	JSR SUBB
SUBD	JSR BSOUT
	JMP CLRCHN
;
SUBB	LDX CNTDN
	CPX #3
	BEQ SUBBR
	LDX WSW
	JMP CHKOUT
;
SUBC	LDX #14         ;DISK CHANNEL
	JSR CHKIN
	JMP BASIN
;
.PAGE 'DOS - DOPEN,APPEND'
;
;ENTRY LOOK UP SUBROUTINE
;
ENTRY0	LDY #$61        ;CALC SA >APPEND ENTRY
ENTRY1	INY
	TYA
	LDX LDTND
ENTRY2	DEX
	BMI RFOUND      ;ALL THRU STK?
	CMP SAT,X
	BEQ ENTRY1      ;THIS SA USED
	BNE ENTRY2      ;CONT DOWN
RFOUND	STY SA          ;.Y=SA
SUBBR	RTS
;
; DOPEN CODE D:FN[,T[,R]]
;
DOPENZ	JSR CHK6        ;PARSE AND CHK INITAL PRAMS
	AND #$22
	BNE SNERRD      ;ADDITIONAL GARBAGE
	JSR ENTRY0      ;GET SA
	LDY #4          ;INFO OFFSET
	LDA #3          ;BASIC LENGTH
	BIT PARCHK      ;REL RECORD?
	BVC LEAV        ;Z=NO
RECLCK	LDA #7          ;LENGTH NOW 7
LEAV	JSR DATSYM      ;BUILD COMMAND AND CHK FOR @ SYM
	JMP OP94
;
.SKI
;APPEND CODE
;
APPENZ	JSR CHK6        ;PARSE & CHK REQ'D PARMS
	AND #$E2        ;CHK OPT PARMS
	BNE SNERRD      ;ADDITIONAL PARMS
	JSR ENTRY0      ;GET A SA
	LDY #11         ;TABLD INDEX
	LDA #5          ;LENGTH
	BNE LEAV        ;DON'T HAVE A @ - JMP LEAV
;
SNERRD	JMP SNERR       ;SYNTAX ERROR
.PAGE 'DOS - ERROR CHANNEL'
;
;ERROR CHANNEL READ
;
ERRCHL	LDA DSDESC      ;ENTRY FOR USER
	BNE ECHKS       ;CHK OLD STATUS
GETDSZ	LDA #40         ;GET 40 CHAR STR
	STA DSDESC      ;WE HAVE 40 CHRS
	JSR GETSPA
	STX DSDESC+1    ;RETURN LOW
	STY DSDESC+2    ;RETURN HIGH
	LDA #00         ;BACKLINK STRING
	STA SATUS       ;CLR STATUS FLAGS
	LDY #41
	STA (DSDESC+1)Y ;UPPER
	DEY
	LDA #<DSDESC    ;ADDRESS $000D
	STA (DSDESC+1)Y ;LOWER
ECHKS	LDA FA          ;CHK BUS <=DS READ
	BNE EREAD       ;IF =0 DEFAULT
	LDA #8
	STA FA
EREAD	JSR TALK
	LDA #$6F
	STA SA
	JSR TKSA
	LDY #$FF;FOR OFFSET
LOOP1	INY
	JSR ACPTR
	CMP #CR         ;CHECK FOR END
	BEQ ERREND
	STA (DSDESC+1)Y
	CPY #39         ;MAKE SURE NOT PAST END
	BNE LOOP1       ;BR ALWAYS
ERREND	LDA #00
	STA (DSDESC+1)Y
	JMP UNTLK       ;GOT ALL
;
.PAGE 'DOS - FORMAT'
;FORMAT ND:DN[,ID]
;
FORMAZ	JSR CHK1        ;PARSE & CHK OPT PARMS
	AND #$11        ;CHK REQ'D PARMS
	CMP #$11
	BEQ DFORMA
FERR0	JMP SNERR
DFORMA	JSR DCLALL      ;CLOSE ALL FILES
	JSR RUSURE      ;R-U-SURE PRPT
;
	LDY #16         ;TABLD INDEX
	LDA #4          ;LENGTH
	LDX DISKID      ;CHECK FOR DISKID
	BEQ FCONT
	LDA #6          ;LENGTH WITH ID
FCONT	JSR TRANS       ;BUILD AND SEND
	JSR DDIREC      ;CHK FOR DIRECT
	BNE DCLBYE
FERRS	JSR ERRCHL      ;ERRS?
	LDY #0
	LDA (DSDESC+1)Y
	CMP #'2
	BCC DCLBYE
	LDY #MSG31-MSG1 ;SEND ?BAD DISK
	JMP MSG
;
DCLBYE	RTS             ;NEED A RETURN
;
.PAGE 'DOS - DCLOSE,RECORD'
;DCLOSE
;
DCLOSZ	JSR DOSPAR
	AND #$F3        ;CHECK OPT PARMS
	BNE FERR0       ;EXTRA STUFF ERR?
DCLSE	JSR OLDCLR
	LDA LA          ;CHK FOR ALL DEFALT
	BEQ DCLALL
	JMP CLOS5       ;GO REMOVE
;
DCLALL	LDA FA          ;GET DISK #
	LDX LDTND       ;SRCH FOR DISK REFS
DCLLP	DEX
	BMI DCLBYE      ;DONE?
	CMP FAT,X
	BNE DCLLP
	LDA LAT,X       ;GET LA & REMOVE
	JSR CLOS10      ;.X=POS .A=LA
	JMP DCLALL
;
.SKI
;DRECORD
;--P<CHL><RECNUML><RECNUMH><POS>
;
BOBREC	LDA LA          ;CHK TABLES
	JSR JLTLK
	BEQ DRECG
	JMP FNFERR      ;FILE NOT FOUND ERR
DRECG	JSR JZ100       ;GET RECORD
	JSR OLDCLR
DREBLD	LDA #'P         ;BUILD
	STA TBUFF
	LDA SA          ;GET CHL
	STA TBUFF+1
	LDA POKER       ;GET RECNUML
	STA TBUFF+2
	LDA POKER+1     ;GET RECNUMH
	STA TBUFF+3
	LDA POS         ;GET POS
	STA TBUFF+4
	LDX #05         ;FNLEN
	JSR TRANR       ;SET POINTERS
	JMP TRANS1      ;CHNL 15
;
.PAGE 'DOS - COLLECT'
;COLLECT V<DRIVE#>
;
COLECZ	JSR DOSPAR
	AND #$E7        ;CHK PARMS
	BNE BERR0
DCOLLE	JSR DCLALL      ;CLOSE ALL FILES
	LDY #22         ;TABLD OFFSET
	LDX #1          ;LENGTH
	LDA PARCHK
	AND #$10
	BEQ DCOLL0
	INX             ;INCLUDE DRIVE
DCOLL0	TXA             ;PLACE IN A
	JMP TRANS
;
.PAGE 'DOS - BACKUP,COPY,CONCAT'
;BACKUP D<DD>=<SD>
;
BACKUZ	JSR DOSPAR
	AND #$30        ;REQ'D PARMS
	CMP #$30
	BEQ BBACK
BERR0	JMP SNERR
BBACK	LDA PARCHK      ;MISSING PARMS ERR?
	AND #$C7        ;OPT PARMS
	BNE BERR0
DBACKU	JSR RUSURE
	JSR DCLALL      ;CLOSE DISK
	LDY #24
	LDA #4          ;LENGTH
;
;TRANS SUBROUTINE
;
TRANS	JSR SENDP       ;BUILD
TRANS1	LDA #$6F        ;TALK TO CHL 15
	STA SA
	JSR LISTN       ;DON'T OPEN15 IT'S ALREADY OPEN
	LDA SA
	JMP OPENIB      ;SEND MESSAGE
;
.SKI
;COPY ROUTINES CDD:DFN=SD:SFN
;
COPYZZ	JSR DOSPAR      ;PARSE IT
	AND #$30
	CMP #$30        ;CHK REQ'D PARMS
	BNE COPY2
	LDA PARCHK
	AND #$C7
	BEQ COPY3
COPY2	LDA PARCHK
	JSR CHK4
COPY3	LDY #28         ;TABLD OFFSET
	LDA #8          ;LENGTH
	JMP TRANS       ;GO DO IT
;
;CONCAT ROUTINES
;--CDD:DFN=DD:DFN,SD:SFN
;
CONCAZ	JSR DOSPAR      ;PARSE IT
	JSR CHK4
	LDY #36         ;OFFSET
	LDA #12         ;LENGTH
	JMP TRANS       ;GO DO IT
;
.PAGE 'DOS - COPY ROUTINES'
;COPY ROUTINES
;
RSFN	LDA FNLEN
	STA FNLEN2
	LDA FNADR
	STA FNADR2
	LDA FNADR+1
	STA FNADR2+1
;
;MOVE INFO INTO TBUFF
;
RDFN	TYA             ;SAVE Y
	PHA
	LDY FNLEN2      ;CHK FOR NO FILENAME
	BEQ RDRT0
	LDY #0          ;MOVE NAME TO TBUFF
RDMOV	LDA (FNADR2)Y
	STA TBUFF,X
	INX
	INY
	CPY FNLEN2      ;ALL DONE?
	BNE RDMOV
	BEQ RDRT1       ;BRA AROUND
RDRT0	DEX             ;CASE CDD=SD
RDRT1	PLA             ;RESTORE Y
	TAY
	SEC             ;RETURN FLAG
RDRT2	RTS
;
; ID SUBROUTINE
;
RID	LDA DISKID      ;INCLUDE ID
	STA TBUFF,X
	INX
	LDA DISKID+1
	STA TBUFF,X
	INX
	TXA             ;KILL WEIRD .A JUST IN CASE
	RTS
;
.PAGE 'DOS - DSAVE,DLOAD,RENAME'
;DSAVE D:FN
;
DSAVEZ	JSR CHK2        ;PARSE AND CHECK INITAL ERRORS
	AND #$66
	BNE SNERRE      ;EXTRA PRAMS
	LDY #4          ;TABLE OFFSET
	LDA #3          ;LENGTH
	JSR DATSYM      ;DO STRING BUILD WITH @ CASE
	JMP SV3         ;DAVE
;
; DLOAD D:FN
;
DLOADZ	JSR CHK1        ;PARSE AND CHECK INITAL ERRORS
	LDY #4
	LDA #3          ;LENGTH
	JSR SENDP       ;BUILD
	LDA #0          ;CLR VERIFY FLAG
	STA VERCK
	JMP LOADNP
;
SNERRE	JMP SNERR       ;ANOTHER SYNTAX ERROR
;
;RENAME RDD:DFN=SD:SFN
;
RENAMZ	JSR DOSPAR
	JSR CHK5
	AND #$E4
	BNE SNERRE
DREN1	LDY #48         ;OFFSET
	LDA #8          ;LENGTH
	JMP TRANS       ;GO DO IT
;
;SCRATCH SD:FN
;
SCRTCZ	JSR CHK1        ;PARSE AND CHK PRAMS
DSCRAT	JSR RUSURE      ;DIRECT=>R-U-SURE
	LDY #56         ;OFFSET
	LDA #4          ;LENGTH
	JSR TRANS
;
NUMSCR	JSR DDIREC      ;DIRECT?
	BNE ANSBYE      ;Z CLR-NOT DIRECT
	JSR ERRCHL      ;READ ERRS
	JSR CRDO        ;OUTPUT LEADING C/R
	LDA DSDESC+1    ;PRINT DS MESSAGE
	LDY DSDESC+2
	JSR STROUT
	JMP CRDO        ;OUTPUT TRAILING C/R
.PAG 'DOS - RUSURE,OLDCLR'
;R-U-SURE SUBROUTINE
RUSURE	JSR DDIREC      ;CHK FOR DIRECT
	BNE ANSYES      ;Z CLR=NOT DIRECT
RUSUR1	LDY #MSG30-MSG1 ;ARE YOU SURE ?
	JSR MSG
	JSR CLRCH       ;CLR CHL FOR BASIN
RUSUR2	JSR BASIN       ;GET BASIC INPUT
	CMP #'Y         ;MUST START WITH Y
	BNE ANSNO
	JSR BASIN       ;NEXT CHR
	CMP #$D         ;CR?
	BEQ ANSYES      ;Y CR
	CMP #'E         ;THEN E
	BNE ANSNO
	JSR BASIN       ;NEXT CHR
	CMP #'S         ;THEN S
	BNE ANSNO
	JSR BASIN       ;NEXT CHR
	CMP #$D         ;CR?
	BEQ ANSYES
ANSNO	JMP READY       ;GAVE PROMPT - GOT WRONG ANSWER
ANSYES
ANSBYE	RTS
;
;OLDCLR SUBROUTINE
;CLEARS DISK STATUS & SATUS
;
OLDCLR	TYA             ;SAVE Y
	PHA
	LDA DSDESC      ;CHK FOR ALLOCATION
	BEQ OLDCL1      ;BRA IF NOT
	LDY #40
	TYA
	STA (DSDESC+1)Y ;LENGTH OF GARBAGE
	INY
	LDA #$FF
	STA (DSDESC+1)Y ;GARBAGE FLAGED
OLDCL1	LDA #0
	STA SATUS       ;ZAP STATUS
	STA DSDESC      ;KILL DS$
	PLA             ;RESTORE Y
	TAY
	RTS
;
.PAGE 'SENDP ROUTINE'
;
DATSYM	BIT PARCHK      ;CHK FOR @
	BPL SENDP
	LDX #'@         ;PUT IN LEADING @
	STX TBUFF
	LDX #01         ;INCREMENT INDEX
	.BYT $2C        ;SKIP
;
SENDP	LDX #0          ;ZERO OFFSET
SENDP1	STA COUNT0      ;NON ZERO ENTRY
	JSR OLDCLR      ;CLR OLD STATUS
SENDP2	DEC COUNT0
	BMI TRANR
	INY             ;MOVE DOWN TABLE
	LDA TABLD,Y     ;GET NEXT ENTRY
	BPL RPLCE       ;IF N CLR THEN CHAR
RXFN1	CMP #XFN1       ;FILE NAME 1
	BNE RXFN2
	JSR RSFN
RXFN2	CMP #XFN2
	BNE RXREC
	JSR RDFN
RXREC	CMP #XREC
	BNE RXID
	LDA LRECL       ;GET REC#
	BNE RPLCE       ;BRA
RXID	CMP #XID
	BNE RXWRT
	JSR RID
RXWRT	CMP #XWRT       ;CHK FOR W OR L
	BNE RXD1
	JSR RWRT
	BNE RPLCE       ;BRA
RXD1	CMP #XD1
	BNE RXD2
	LDA DRIVE1
	BPL RXDD        ;BRA
RXD2	CMP #XD2
	BNE SENDP2      ;END OF TABLE BRA
	LDA DRIVE2
RXDD	ORA #48         ;CHANGE TO ASCII
;
RPLCE	STA TBUFF,X     ;ELSE INTO BUFFER
	INX
	BNE SENDP2      ;BRA TO RESTART
;
TRANR	STX FNLEN       ;X IS LENGTH TO SEND
	LDA #<TBUFF     ;MOVE POINTERS
	STA FNADR
	LDA #>TBUFF
	STA FNADR+1
	RTS
.PAG 'DOS - RWRT'
RWRT	LDA LRECL       ;CHK FOR L OR W
	BEQ RWRT1       ;ZERO THEN WRITE
	LDA #'L
	BNE RWRTS       ;BRA
RWRT1	LDA #'S         ;SEND W,S
	STA LRECL
	LDA #'W
RWRTS	RTS
.END
