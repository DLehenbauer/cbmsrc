.PAG 'PROGRAM KEYS'
KEYPGM	JSR CHKCOM
	JSR GETBYT
	TXA
	CMP #10
	BCS KERX2
	PHA
	JSR CHKCOM
	LDA #$F0        ;DEFAULT TARGET TYPE
	STA TARGET
	JSR FRMEVL
	LDA TARGET
	BPL KERX1
	JSR FRESTR
	TAY
	PLA
	TAX
	TYA
	JSR PGMKEY
	BCC KERX3       ;BRANCH IF NOT ENOUGH ROOM
	RTS
KERX3	LDX #ERRLS      ;STRING TOO LONG ERROR
	.BYT $AD        ;SKIP 2
KERX1	LDX #ERRFC      ;ILLEGAL QUANTITY
	.BYT $AD        ;SKIP 2
KERX2	LDX #ERRTM      ; TYPE MISMATCH
	JMP ERROR
.SKI
;******************************
;
; LIST THE PROGRAMABLE KEYS
;
;******************************
.SKI
LSTKEY	LDA #9          ;LIST ALL 9
	STA INDEX3
LSTK10
	LDY #3
FIRSTK	LDA KEWORD,Y    ;PRINT "KEY "
	JSR BSOUT
	DEY
	BPL FIRSTK
.SKI
	LDA INDEX3
	ORA #$30        ;MAKE IT ASCII
	JSR BSOUT
	JSR PUTKEY
	LDA #',
	JSR BSOUT
	LDA #'"
	JSR BSOUT
	LDA KYNDX
	BEQ NULLSO
	LDY #0
LSTK20	LDA (KEYPNT)Y
	JSR BSOUT
	INY
	DEC KYNDX
	BNE LSTK20
.SKI
NULLSO	LDA #'"
	JSR BSOUT
	JSR CRDO
	DEC INDEX3
	BPL LSTK10
	RTS
KEWORD	.BYT ' YEK'
