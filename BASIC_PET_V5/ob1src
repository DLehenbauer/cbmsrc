.PAG 'OB1SRC'
;7000 OFFSET
MSG1
MS1	.BYT 'TOO MANY FILES',0
MS2	.BYT 'FILE OPEN',0
MS3	.BYT 'FILE NOT OPEN',0
MS4	.BYT 'FILE NOT FOUND',0
MS5	.BYT $D,'SEARCH'
MS20	.BYT 'ING',$A0
MS6	.BYT 'FOR',$A0
MS7	.BYT $D,'PRESS PLAY',$A0
MS8	.BYT '& RECORD',$A0
MS9	.BYT 'ON TAPE ',$A3
MS10	.BYT $D
MS22	.BYT 'LOA',$C4
MS11	.BYT $D,'WRITING',$A0
MS21	.BYT $D
MS12	.BYT 'VERIF',$D9
MS13	.BYT 'DEVICE NOT PRESENT',0
MS15	.BYT 'NOT INPUT FILE',0
MS16	.BYT 'NOT OUTPUT FILE',0
MS17	.BYT $D,'FOUND',$A0
MS18	.BYT $D,'OK',$8D
MS19	.BYT $D,'READY.',$8D
MSG30	.BYT $D,'ARE YOU SURE ',$BF
MSG31	.BYT $D,'BAD DIS',$CB
TALK	LDA #$40
	.BYT $2C        ;SKIP TWO BYTES
LISTN	LDA #' 
LIST1	PHA
	LDA PIA
	ORA #2
	STA PIA
	LDA #$3C
	STA IEEIS
	BIT C3P0
	BEQ LIST2
	LDA #$34
	STA PIAL1
	JSR ISOUR
	LDA #0
	STA C3P0
	LDA #$3C
	STA PIAL1
LIST2	PLA
	ORA FA
	STA BSOUR
OLIST4	LDA PIA
	BPL OLIST4
	AND #$FB
	STA PIA
ISOUR	LDA #$3C
	STA IEEOS
	LDA PIA
	AND #$41
	CMP #$41
	BEQ ERRP7
	LDA BSOUR
	EOR #$FF
	STA IEEO
ISR1	BIT PIA
	BVC ISR1
	LDA #$34
	STA IEEOS
ISR0	LDA #$FF
	STA TIH
ISR2	LDA PIA
	BIT IFR
	BVS ERRS3
	LSR A
	BCC ISR2
ISR3	LDA #$3C
	STA IEEOS
	LDA #$FF
	STA IEEO
	RTS
SECND	STA BSOUR
	JSR ISOUR
SCATN	LDA PIA
	ORA #4
	STA PIA
	RTS
ERRS3	LDA BOB         ;CHECK TIMEOUT FLAG
	BPL ERRP0
	JSR STOP
	BNE ISR0
ERRS4	LDA BOB
	BPL ERRP1
	JSR STOP
	BNE ACP00
ERRP0	LDA #1
ERR01	JSR UDST
	BNE ISR3
ERRP7	LDA #$80
	BMI ERR01
ERRP1	LDA #2
	JSR UDST
ER001	LDA PIA
	AND #$FD
	STA PIA
	LDA #$34
	STA IEEIS
	LDA #$0D
	RTS
MSG	LDA MS1,Y
	PHP
	AND #$7F
	JSR PRT
	INY
	PLP
	BPL MSG
	RTS
TKSA	STA BSOUR
	JSR ISOUR
TKATN	JSR ER001
	JMP SCATN
CIOUT	BIT C3P0
	BMI CI2
	DEC C3P0
	BNE CI4
CI2	PHA
	JSR ISOUR
	PLA
CI4	STA BSOUR
	RTS
UNTLK	LDA PIA         ;PULL ATN FIRST
	AND #$FB
	STA PIA 
	LDA #$5F
	.BYT $2C        ;SKIP TWO BYTES
UNLSN	LDA #$3F
	JSR LIST1
	BNE SCATN
ACPTR	LDA #$34
	STA IEEIS
	LDA PIA
	ORA #2
	STA PIA
ACP00	LDA #$FF
	STA TIH
ACP01	BIT IFR
	BVS ERRS4
	BIT PIA
	BMI ACP01
	LDA PIA
	AND #$FD
	STA PIA
	BIT PIAL
	BVS ACP03
	LDA #$40
	JSR UDST
ACP03	LDA IEEI
	EOR #$FF
	PHA
	LDA #$3C
	STA IEEIS
ACP05	BIT PIA
	BPL ACP05
	LDA #$34
	STA IEEIS
	PLA
	RTS
GETIN	LDA #0
	STA SATUS
	LDA DFLTN
	BNE BN10
	LDA NDX
	BEQ BN32
	SEI
	JMP LP2
BASIN	LDA DFLTN
	BNE BN10
	LDA PNTR
	STA LSTP
	LDA TBLX
	STA LSXP
	JMP LOOP5
BN10	CMP #3
	BNE BN20
	STA CRSW
	LDA LNMX
	STA INDX
	JMP LOOP5
BN20	BCS BN30
	STX XSAV
	JSR JTGET
	PHA
	JSR JTGET
	BNE JTG35
	LDA #64
	JSR UDST
JTG35	DEC BUFPT-1,X
	LDX XSAV
	PLA
	RTS
JTGET	JSR JTP20
	BNE JTG10
	JSR RBLK
	LDX FA
	LDA #0
	STA BUFPT-1,X
	BEQ JTGET
JTG10	LDA (TBUF)Y
	RTS 
BN30	LDA SATUS
	BEQ BN35
	LDA #$D
BN32	RTS
BN35	JMP ACPTR
BSOUT	PHA
	LDA DFLTO
	CMP #3
	BNE BO10
	PLA
	JMP PRT
BO10	BMI BO20
	PLA
	JMP CIOUT
BO20	PLA
BO21	STA T1
	CMP #$A
	BEQ BN32
	PHA
	TXA
	PHA
	TYA
	PHA
	JSR JTP20
	BNE JTP10
	JSR WBLK
	LDX FA
	LDA #1
	STA BUFPT-1,X
	LDY #0
	LDA #BDF
	STA (TBUF)Y
	INY
JTP10	LDA T1
	STA (TBUF)Y
RSTOR	PLA
	TAY
	PLA
	TAX
	PLA
	RTS
CLALL	LDA #0
	STA LDTND
CLRCHN
CLRCH	LDA DFLTO
	CMP #4
	BCC JX750
	JSR UNLSN
JX750	LDA DFLTN
	CMP #4
	BCC JX770
	JSR UNTLK
JX770	LDA #3
	STA DFLTO
	LDA #0
	STA DFLTN
	RTS
JLTLK	LDX LDTND
JX600	DEX
	BMI JZ101
	CMP LAT,X
	BEQ JZ101
	BNE JX600
JZ100	LDA LAT,X
	STA LA
	LDA FAT,X
	STA FA
	LDA SAT,X
	STA SA
JZ101	RTS
.END
