.PAG 'SCREEN4 5/12/80'
;*=BEGIN+$0442
;
; ++++++ MUST START @ $E442 ++++++
;
PULS	PHA             ;IRQ HANDLER
	TXA             ;SAVE REGS.
	PHA
	TYA
	PHA
	TSX
	LDA $104,X
	AND #$10        ;A BREAK ?
	BEQ PULS1       ;NO - THEN DI IRQ
	JMP (CBINV)
PULS1	JMP (CINV)
;
; ++++++ MUST START @ $E455 ++++++
;
KEY	JSR $FFEA       ;UPDATE JIFFY CLOCK
	LDA BLNSW       ;BLINKING CRSR ?
	BNE KEY4        ;NO
	DEC BLNCT       ;TIME TO BLINK ?
	BNE KEY4        ;NO
	LDA #20         ;RESET BLINK COUNTER
	BIT RPTFLG      ;REPEAT KEY
	BPL REPDO       ;NO
	LDA #2          ;YES - FLASH CRSR
REPDO	STA BLNCT
	LDY PNTR
	LSR BLNON
	LDA (PNT),Y     ;GRAB CHAR ON SCREEN
	BCS KEY5        ;SKIP IF NOT ORIGINAL
	INC BLNON       ;SET FLAG GOT ORIGINAL
	STA GDBLN
KEY5	EOR #$80        ;BLINK CRSR
	STA (PNT),Y
KEY4	LDY #0
	LDA PIAL        ;TEST CASSETTE SWITCHS
	AND #$F0
	STA PIAL
	LDA PIAL
	ASL A
	ASL A
	ASL A
	BPL KEY3
	STY CAS1
	LDA PIAS        ;CASS #1 MOTOR OFF
	ORA #$08
	BNE KL24
KEY3	LDA CAS1
	BNE KL2
	LDA PIAS        ;CASS #1 MOTOR ON
	AND #$F7
KL24	STA PIAS
KL2	BCC KL23
	STY CAS2
	LDA PIA         ;CASS #2 MOTOR OFF
	ORA #$10
	BNE KL25
KL23	LDA CAS2
	BNE KL26
	LDA PIA         ;CASS #2 MOTOR ON
	AND #$EF
KL25	STA PIA
.SKI 2
KL26	JSR SCNKEY      ;SCAN KEYBOARD AND FILL BUFFER
	JMP PREND       ;EXIT FROM IRQ
.SKI 3
; ****** GENERAL KEYBOARD SCAN ******
;
SCNKEY	LDY #$FF
	STY SFDX        ;NULL NEW GET INDEX
	INY             ;Y=0
	STY SFST        ;CLEAR SHIFT FLAG
	LDA RPTFLG
	AND #$7F
	STA RPTFLG      ;CLEAR REPEAT FLAG
	LDX #80         ;SCAN 80 KEYS
SCN20	LDY #8          ;8 ROW KEYBOARD
	LDA PIAK
	CMP PIAK        ;DEBOUNCE KEYBOARD
	BNE SCN20
SCN30	LSR A           ;LOOK FOR KEY DOWN
	BCS CKIT        ;NONE
	PHA
	LDA CHARB-1,X   ;GET BUSINESS MATRIX
	BNE SPCK        ;REAL KEY
	LDA #1          ;SET SHIFT FLAG
	STA SFST
	BNE CKUT        ;SKIP TO CKUT
SPCK	CMP #$10        ;RPT KEY?
	BNE SPCK2       ;NO
	LDA RPTFLG
	ORA #$80
	STA RPTFLG
	BMI CKUT        ;BRANCH ALWAYS
SPCK2	CMP #$FF        ;A NULL KEY ?
	BEQ CKUT        ;YES - IGNORE IT
	STA SFDX        ;NO - SAVE NEW KEY
CKUT	PLA
CKIT	DEX
	BEQ CKIT1
	DEY
	BNE SCN30
	INC PIAL        ;NEXT COLUMN ON KEYBOARD
	BNE SCN20       ;ALWAYS BRANCH
CKIT1	LDA SFDX
	CMP LSTX        ;SAME AS PREV CHAR?
	BEQ RPT10       ;YES
	LDX #$10        ;NO - RESET DELAY BEFORE REPEAT
	STX DELAY
	BNE CKIT2       ;ALWAYS
RPT10	BIT RPTFLG      ;REPEAT ?
	BMI RPT40       ;YES
	BVS SCNRTS
	CMP #$FF        ;NO KEYS ?
	BEQ SCNRTS      ;YES - GET OUT
	CMP #$14        ;AN INST/DEL KEY ?
	BEQ RPT20       ;YES - REPEAT IT
	CMP #$20        ;A SPACE KEY ?
	BEQ RPT20       ;YES
	CMP #$1D        ;A CRSR LEFT/RIGHT ?
	BEQ RPT20       ;YES
	CMP #$11        ;A CRSR UP/DWN ?
	BNE SCNRTS      ;NO - EXIT
RPT20	LDX DELAY       ;TIME TO REPEAT ?
	BEQ RPT40       ;YES
	DEC DELAY
	BNE SCNRTS
RPT40	DEC RPTCNT      ;TIME FOR NEXT REPEAT ?
	BNE SCNRTS      ;NO
	LDX #4          ;YES - RESET CTR
	STX RPTCNT
	LDX NDX         ;NO REPEAT IF QUEUE FULL
	DEX
	BPL SCNRTS
CKIT2	STA LSTX
CKIT3	CMP #$FF        ;A NULL KEY OR NO KEY ?
	BEQ SCNRTS      ;YES - IGNORE IT
CKIT4	TAX             ;SET MINUS FLAG
	PHP
	AND #$7F
	PLP
	BMI KN1
	LSR SFST        ;A SKIFT KEY ?
	BCC KN1         ;NO - SKIP
	CMP #$2C
	BCC KN2
	CMP #$3C
	BCS KN2
	SBC #15
	CMP #$20
	BCS KN1
	ADC #32
	.BYT $2C        ;SKIP TO KN1
KN2	ORA #$80
KN1	LDX NDX
	CPX XMAX        ;IRQ BUFFER FULL ?
	BCS SCNRTS      ;YES - NO MORE INSERT
	CMP #$12        ;IS IT AN RVS KEY?
	BNE PUTQUE      ;BRANCH IF NOT
	LDY RVSESC      ;ELSE CHECK IF WE SHOULD CHANGE TO ESC
	BEQ PUTQUE      ;BRANCH IF NOT
	LDA #$1B        ;CHANGE RVS TO ESC
PUTQUE
	STA KEYD,X      ;NO - PUT CHAR IN
	INX
	STX NDX
SCNRTS	RTS
.SKI 4
;
;TAB SET-UP (TAB POSITIONER)
GETTAB	LDA PNTR        ;FIND CURSOR POS
	AND #$F8        ;ERASE LOWER BITS
	STA TABPOS
	LSR A           ;DIVIDE BY 8
	LSR A
	LSR A
	TAX             ;LOAD INDEX
	LDA #1          ;SET BIT MASK
	STA BITMSK
	LDY PNTR
TABOK	CPY TABPOS      ;IS TAB BIT AT CURSOR POS?
	BEQ RTSTAB      ;YES => RET
	ASL BITMSK      ;ELSE ROTATE BIT MASK
	INC TABPOS      ;GOTO NXT TABPOS
	JMP TABOK       ;TRY AGAIN
RTSTAB	RTS
.SKI 3
; ****** MORE OF PRT ROUTINE ******
;
SCTUP	JMP LOOP2
.SKI 3
;* =BEGIN+$600
; ++++++ MUST START @ $E600 ++++++
;
PREND	PLA             ;RESTORE REGS.
	TAY
	PLA
	TAX
	PLA
	RTI
.SKI 3
DSPP	LDY PNTR
	STA (PNT),Y
	LDA #2
	STA BLNCT       ;FORCE CRSR TO BLINK
	RTS
.SKI 3
; ****** I/O AND VARABLE SETUP ******
;
EXTRA0	LDA #$7F        ;EXTRA I/O
	STA IER
	LDX #CAS2-TIME
	LDA #0
	STA FHOME       ;CLEAR FOR FIRST HOME
	STA INSFLG      ;AUTO INSERT OFF
	STA RPTFLG      ;ALLOW REPEAT CURSOR
PX1	STA TIME,X      ;ZERO ALL VARABLES
	DEX
	BPL PX1
	LDA #<KEY       ;SET IRQ VECTOR
	STA CINV
	LDA #>KEY
	STA CINV+1
	LDA #9          ;SET MAX IRQ KEY BUF TO 9
	STA XMAX
	LDA #3          ;OUTPUT DEVICE SCREEN
	STA DFLTO
	LDA #$F         ;KEYBOARD SCANER
	STA PIAL
	ASL A
	STA PIA
	STA P2DB
	STX IEEO
	STX TIH
	LDA #$3D        ;ENABLE 60HZ IRQ
	STA PIAS
	BIT PIAK
	LDA #$3C
	STA IEEIS
	STA IEEOS
	STA PIAL1
	STX IEEO
	LDA #$E
	STA BLNCT
	STA BLNSW
	STA DELAY       ;INIT REPEAT DELAY
	STA RPTCNT
	STA IER
	JSR SRESET      ;RESET SCROLL WINDOW
CLRTAB	LDX #9          ;CLEAR TAB ENTRANCE
	LDA #0
CLRALL	STA TAB,X       ;CLR ALL TAB KEYS
	DEX
	BPL CLRALL
	STA SCRDIS      ;ENABLE SCROLLING
	STA KYNDX       ;0 CHARS IN PROGRAMABLE KEY BUF
	STA LOGSCR      ;TURN ON LOGICAL LINE SCROLL
	STA RVSESC      ;DEFAULT RVS KEY TO RVS FUNCTION
	LDA #<LOP55     ;SET INDIRECT FOR INPUT
	LDX #>LOP55
	STA VINP
	STX VINP+1
	LDA #<PRT55     ;SET INDIRECT FOR OUTPUT
	LDX #>PRT55
	STA VOUT
	STX VOUT+1
	LDA #<CLP7      ;SET UP SCROLL INDIRECTS TO RTS
	LDX #>CLP7
	STA UPINDR
	STX UPINDR+1
	STA DNINDR
	STX DNINDR+1
	LDA #$10        ;ENABLE BELL SOUND
	STA FBEL
BEL4	JSR BEL2        ;SOUND 4 BELLS
	BEQ BEL2        ;ALWAYS
.SKI 4
PATCH2	JSR PRT         ;PRINT THE CHAR
	TAX             ;SAVE IT
	LDA SCRT        ;GET MAX COLUMN
	SEC
	SBC PNTR        ;GET DIFF FROM END
	CMP #5          ;FIVE AWAY ?
	BNE BEL30       ;NO
	TXA             ;YES
	CMP #$1D        ;A CRSR RIGHT ?
	BEQ BEL2        ;YES - SOUND BELL
	AND #$7F        ;REMOVE SHIFT KEY
	CMP #$20        ;ANY OTHER CTRL KEY ?
	BCC BEL30       ;YES - SOUND NOT
.SKI 3
; *** BELL ***
;
BEL2	JSR BELL        ;RING BELL TWICE
BELL	LDY FBEL        ;BELL ENABLED ?
	BEQ BEL30       ;NO
	LDA #$10        ;ENABLE CB2 SHIFT BY T2
	STA ACR
	LDA #$0F        ;SQUARE WAVE IN SHIFT REG.
	STA SR
	LDX #7          ;NINE NOTES
BEL10	LDA TONES-1,X   ;GET TONE
	STA T2L         ;PLAY IT
	LDA FBEL        ;DELAY
BEL20	DEY
	BNE BEL20
	SEC
	SBC #1
	BNE BEL20
	DEX
	BNE BEL10
	STX SR
	STX ACR
BEL30	RTS
.END
