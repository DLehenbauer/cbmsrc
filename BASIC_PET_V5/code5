.PAG 'CODE5'
RETURN	BNE GORTS
	LDA #255
	STA FORPNT+1
	JSR FNDFOR
	TXS
	CMP #GOSUTK
	BEQ RETU1
	LDX #ERRRG
	.BYT $2C
USERR	LDX #ERRUS
	JMP ERROR
SNERR2	JMP SNERR
RETU1	PLA
	PLA
	STA CURLIN
	PLA
	STA CURLIN+1
	PLA
	STA TXTPTR
	PLA
	STA TXTPTR+1
DATAB	JSR DATAN
ADDON	TYA
	CLC
	ADC TXTPTR
	STA TXTPTR
	BCC REMRTS
	INC TXTPTR+1
REMRTS	RTS
DATAN	LDX #':
	.BYT $2C
REMN	LDX #0
REMN1	STX CHARAC
	LDY #0
	STY ENDCHR
EXCHQT	LDA ENDCHR
	LDX CHARAC
	STA CHARAC
	STX ENDCHR
REMER	LDA (TXTPTR)Y
	BEQ REMRTS
	CMP ENDCHR
	BEQ REMRTS
	INY
	CMP #34
	BNE REMER
	BEQ EXCHQT
IF	JSR FRMEVL
	JSR CHRGOT
	CMP #GOTOTK
	BEQ OKGOTO
	LDA #THENTK
	JSR SYNCHR
OKGOTO	LDA FACEXP
	BNE DOCOND
	LDX #0          ;ELSE TOKEN SEARCH $$$$$$$$$$$$$$$$$$$$$$$$
	LDY #0
	STY CHARAC      ;ZERO LEVEL
ELX1	LDA (TXTPTR)Y   ;GET CHAR
	BEQ ADDON       ;END OF LINE
	INY
	CMP #'"
	BEQ ELX4
	CPX #0          ;IN BETWEEN QUOTES?
	BNE ELX1
;
	CMP #IFTK
	BEQ ELX2
	CMP #ELSETK
	BNE ELX1        ;FOUND NOTHING INTRESTING
	DEC CHARAC      ;DELETE ONE LEVEL OF IF-THEN-ELSE
	DEC CHARAC
ELX2	INC CHARAC      ;ADD ONE LEVEL OF IF-THEN-ELSE
	BPL ELX1        ;BRA
	JSR ADDON       ;AT OUTER LEVEL SO...
	JMP DOCOND      ;...DO WHATEVER FOLLOWS
;
ELX4	DEX             ;FLIP QUOTE FLAG
	BEQ ELX1        ;=0 JUST TURNED OFF
	INX             ;=-1 TURN ON
	INX
	BNE ELX1
;
REM	JSR REMN
	BEQ ADDON
DOCOND	JSR CHRGOT
	BCS DOCO
	JMP GOTO
DOCO	JMP GONE3
ONGOTO	CMP #ERRTK      ;ON ERROR STATEMENT?
	BNE ISONI       ;BRANCH IF NOT
	JSR ERRDIR      ;ON GOTO IS ILLEGAL IN DIRECT MODE
	JSR CHRGET      ;ELSE GET NEXT BYTE
	LDA #GOTOTK     ;GET THE GOGO TOKEN
	JSR SYNCHR      ;CHECK NEXT BYTE AGAINST IT
	BNE SYNOK       ;BRANCH IF ITS NOT NULL
ERRGO	JMP SNERR       ;ELSE SYNTAX ERROR
SYNOK	JSR LINGET      ;CONVERT THE ASCII LINE# TO INTEGER
	LDY LINNUM      ;GET THE 1ST BYTE
	STY ONERR       ;PUT IT HERE FOR ERROR TRAP
;(IF WE GET AN ERROR)
	LDY LINNUM+1    ;GET THE SECOND BYTE
	STY ONERR+1     ;SAVE 2ND BYTE AWAY
;
	LDY #$80        ;INDICATE ON ERROR IS ACTIVE
	STY ACTERR      ;SAVE ON ERROR STATUS ACTIVE
	RTS
ISONI	JSR GETBYT      ;EVALUATE EXPRESSION
	PHA
	CMP #GOSUTK
	BEQ ONGLOP
SNERR3	CMP #GOTOTK
	BNE ERRGO
ONGLOP	DEC FACLO
	BNE ONGLP1
	PLA
	JMP GONE2
ONGLP1	JSR CHRGET
	JSR LINGET
	CMP #44
	BEQ ONGLOP
	PLA
ONGRTS	RTS
LINGET	LDX #0
	STX LINNUM
	STX LINNUM+1
	BCC MORLIN      ;$$$$$$$$$$$TEST$$$$$$$$$$$$$
	LDX CURLIN+1    ;$$$$$$$CHK FOR DIRECT$$$$$$$
	INX             ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$
	BEQ ONGRTS      ;$$$$$$$$YES RETURN$$$$$$$$$$
	CMP #0          ;CHK FOR END
	BEQ ONGRTS      ;NO FOLLOWERS
	JSR FRMNUM      ;GET ADDRESS VALUE
	JMP GETADR
MORLIN	BCS ONGRTS
	SBC #$2F
	STA CHARAC
	LDA LINNUM+1
	STA INDEX
	CMP #25
	BCS SNERR3
	LDA LINNUM
	ASL A
	ROL INDEX
	ASL A
	ROL INDEX
	ADC LINNUM
	STA LINNUM
	LDA INDEX
	ADC LINNUM+1
	STA LINNUM+1
	ASL LINNUM
	ROL LINNUM+1
	LDA LINNUM
	ADC CHARAC
	STA LINNUM
	BCC NXTLGC
	INC LINNUM+1
NXTLGC	JSR CHRGET
	JMP MORLIN
.END
