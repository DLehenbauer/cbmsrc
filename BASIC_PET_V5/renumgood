.PAG 'RENUMBER'
;**********************************
;
; TRACE - DISABLE TRACE RSR 5-14-80
; TRACE # - ENABLE TRACE #=DELAY
;
;**********************************
.SKI
TRACE	BEQ TRAC01      ;DISABLE ?
	JSR GETBYT      ;GET VALUE 0-255
	STX TRCE        ;TRACE SPEED
	LDA #0
	STA TRCBDY      ;TRACE TOP
	STA TCPNTR      ;TRACE START COL
	LDX #4          ; 5 LINES OF TRACE 0-4
	STX TRCBDY+1    ;TRACE BOTTOM
	STX TCTBLX      ;TRACE START ROW
	INX
	STX SCTOP       ;SET TOP OF MAIN SCREEN
	LDA SCLF        ;USE CURRENT EDGES - SCREEN INDEPENDENT
	STA TRCBDY+2
	LDA SCRT
	STA TRCBDY+3
	LDA #<TRACE2    ;TRACE ON
	LDX #>TRACE2
	BNE TRAC02      ;BRA
TRAC01	LDA #0          ;RESTORE TO TOP
	STA SCTOP
	LDA #<NGONE     ;TRACE OFF
	LDX #>NGONE
TRAC02	STA IGONE       ;SET INDIRECTS
	STX IGONE+1
PRTRTN	JSR CHRGOT      ;LOOK FOR TERMINATOR
	BEQ PNOERR
PYSERR	JMP SNERR       ;SYNTAX ERROR
PNOERR	RTS             ;MAY BE IN PROGRAM
;
; AID COMMAND
;
; SHOW LAST EXECUTE CHRGOT
;
AID	JSR CRDO        ;PRINT (CR) (LF)
	LDA #$80        ;ENABLE AID
	STA AIDFLG
	LDA CURLIN      ;GET LAST LINE #
	LDX CURLIN+1
LLLL1	STA LINNUM
	STX LINNUM+1
	JSR FNDLIN      ;FIND IT
LLLL0	JMP LIST4       ;TRY THIS OUT
;
; NUMBER COMMAND
;
; SYNTAX
;
; NUMBER N1,N2
; N1=START NUMBER
; N2=INCREMENT SIZE
;
RENUM	LDX #4
	STX ENDCHR      ;TEMP
	TAX             ;SET FLAGS
	BNE REN002
REN001	LDY #10         ;DEFAULT TO 10
	BNE REN003      ;BRA
REN002	JSR LINGET      ;GET NUMBER
	LDA LINNUM+1
	LDY LINNUM
REN003	LDX ENDCHR      ;INDEX
	STY CURLIN,X
	STA CURLIN+1,X
	DEC ENDCHR
	DEC ENDCHR
	BMI REN004      ;DONE?
	JSR CHRGOT      ;NO...SEE LAST CHAR
	BEQ REN001      ;A ZERO...DEFAULT
	JSR CHKCOM      ;ELSE...A COMMA
	BEQ REN001      ;RETURNS...NULL
	BNE REN002      ;A NUMBER
REN004	CMP OLDTXT+1    ;NEWLINE > STARTLINE
	BCC REN005      ;
	BNE PYSERR      ; IF GREATER THEN SYNTAX ERROR
	CPX OLDTXT
	BCC REN005
	BNE PYSERR      ; IF GREATER THEN SYNTAX ERROR
REN005	LDA OLDLIN+1    ;INCREMENT MUST BE <>0
	ORA OLDLIN
	BEQ PYSERR
	JSR FNDLIN      ;FIND STARTLINE
	LDA LOWTR
	STA CURLIN      ;PUT STARLINEPTR IN CURLIN
	LDA LOWTR+1
	STA CURLIN+1
;
RENN	JSR STSTPT      ;TXTPTR=STARTLINPTR
RN10	JSR DGRAB       ;SKIP LINKS -AT END ?
	BNE RN20        ;NO
	JSR SETFAC      ;YES - FACHO=FIRST NEW #
RN17	JSR DGRAB       ;SKIP LINKS - DONE ?
	BNE RN18        ;NO
	JMP NOTDEL      ;GO FIX LINKS
RN18	JSR GRAB        ;GET TO LO LINE #
	LDA FACHO+1     ;PUT NEW LINE # IN
	STA (TXTPTR)Y
	JSR GRAB        ;GET TO HI LINE #
	LDA FACHO
	STA (TXTPTR)Y
	JSR ADDSTP      ;GEN NEXT LINE #,FIND EOL
	BEQ RN17        ;ALWAYS
RN20	JSR DGRAB       ;SKIP LINE #
RN30	JSR GRAB        ;GET BASIC SOURCE
RN35	CMP #'"         ;A QUOTE ?
	BNE RN38        ;NO
RN37	JSR GRAB        ;SKIP STUFF IN QUOTES
	BEQ RN10        ;END OF LINE, GET OUT
	CMP #'"         ;OUT OF QUOTES ?
	BNE RN37        ;NO
	BEQ RN30        ;ALWAYS
RN38	TAX             ;END OF LINE ?
	BEQ RN10        ;YES - LOOP
	BPL RN30        ;SKIP NON-TOKENS
;
;LOOK FOR 6 TOKENS
RN40	CMP #GOTOTK
	BEQ RN50
	CMP #RUNTK
	BEQ RN50
	CMP #GOSUTK
	BEQ RN50
	CMP #RESTTK
	BEQ RN50
	CMP #ELSETK
	BEQ RN50
	CMP #THENTK
	BNE RN30
RN50	LDA TXTPTR      ;SAVE TXTPTR
	STA DATLIN
	LDA TXTPTR+1
	STA DATLIN+1
	JSR CHRGET      ;IS A # FOLLOWING ?
	BCS RN35        ;NO - DONT FIX
	JSR FRMNUM      ;GET LINE#
	JSR GETADR      ;MAKE IT BIN #
	JSR FINUM       ;FIND OLD#,GEN NEW #
	LDA DATLIN+1    ;RESTORE TXTPTR TO BEGIN
	STA TXTPTR+1
	LDA DATLIN
	STA TXTPTR
	LDY #0          ;SET TO MOVE IN NEW #
	LDX #0
RN60	LDA FBUFFR,X    ;GET ASCII NEW #
	BEQ RN70        ;IF ZERO THEN DONE
	PHA
	JSR CHRGET      ;IS CHAR ASCII # ?
	BCC RN65        ;YES - SKIP MOVE
	JSR ONEUP       ;NO - MOVE SRC UP ONE BYTE
RN65	PLA
	LDY #0
	STA (TXTPTR)Y   ;REPLACE OLD
	INX
	BNE RN60        ;ALWAYS
RN70	JSR CHRGET      ;ANY OLD ASCII LEFT ?
RN75	BCS RN80        ;NO - FINISHED
RN78	JSR ONEDWN      ;YES - MOVE SRC DOWN ONE
	JSR CHRGOT      ;TEST NEW CHAR IN OLD PLACE
	BCC RN78        ;MORE TO MOVE
RN80	CMP #',         ;IS THIS AN "ON"
	BEQ RN50        ;YES - DO THE REST
	BNE RN35        ;ALWAYS
;
; FINDS OLD NUMBER IN OLD LINES,
; GENERATES A NEW LINE # AND
; PUTS IT IN ASCII IN FBUFFR
; ENDS IT WITH 00
; ENTER LINNUM=OLD LINE #
;
FINUM	JSR SETFAC      ;FACHO=BEGIN NEW #
FN10	JSR DGRAB       ;SKIP LINKS
	BNE FN30        ;NO
	LDA #$FF        ;YES - DID NOT FIND #
	STA FACHO+1     ;MAKE NUMBER TOO LARGE
	STA FACHO       ;65535 WILL GIVE ERROR
	BMI FN40        ;ALWAYS
FN30	JSR GRAB        ;GET LO LINE #
	CMP LINNUM      ;EQUAL ?
	BNE FN50        ;NO
	JSR GRAB        ;MAYBE - GET HI LINE #
	CMP LINNUM+1    ;EQUAL
	BNE FN60        ;NO
FN40	JMP PAFOT1      ;PRINT ASCII NUMBER
FN50	JSR GRAB        ;SKIP HI LINE #
FN60	JSR ADDSTP      ;GEN NEXT LINE #
	BEQ FN10        ;ALWAYS
;
; MOVES TEXT UP ONE BYTE
;
ONEUP	JSR SETPPP      ;SET INDEX1 & 2
	INC SVXT        ;=1
	JSR MOVEUP      ;MOVE ALL UP ONE
	INC VARTAB      ;ADJUST END PTR
	BNE ONEU10
	INC VARTAB+1
ONEU10	RTS
;
; MOVES TEXT DOWN ONE BYTE
;
ONEDWN	JSR SETPPP      ;SET INDEX1 & 2
	DEC SVXT        ;=FF
	JSR MOVDWN
	LDA VARTAB      ;ADJUST END PTR
	BNE ONED10      ;NO BORROW
	DEC VARTAB+1
ONED10	DEC VARTAB
	RTS
;
; SETS POINTERS INDEX1 & INDEX2
;
SETPPP	JSR SETPTR      ;SET INDEX1 & 2
	LDY #0
	STY COUNT
	STY SVXT
	RTS
;
; SET FACHO+1 AND FACHO TO START
; LINE NUMBER
;
SETFAC	LDA OLDTXT
	STA FACHO+1
	LDA OLDTXT+1
	STA FACHO
STSTPT	LDA CURLIN      ;TXTPTR=STARTLINPTR
	STA TXTPTR
	LDA CURLIN+1
	STA TXTPTR+1
	RTS
;
; ADDS INC SIZE(OLDLIN) TO
; FACHO+1 AND FACHO
;
ADDSTP	LDA FACHO+1     ;GET LO
	CLC
	ADC OLDLIN      ;ADD INC
	STA FACHO+1
	LDA FACHO
	ADC OLDLIN+1
	STA FACHO
ADD200	JSR GRAB        ;END OF LINE ?
	BNE ADD200      ;NO - LOOK ON
	RTS
;
; GET A CHAR AND SET ZERO FLAG
;
DGRAB	JSR GRAB        ;GRAB OFF TWO CHARS
GRAB	LDY #0
	INC TXTPTR      ;BUMP POINTER
	BNE GR10
	INC TXTPTR+1
GR10	LDA (TXTPTR)Y
	RTS
.END
