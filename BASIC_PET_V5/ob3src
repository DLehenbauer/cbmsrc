.PAG 'OB3SRC'
FAH	LDA VERCK
	PHA
FAH30	JSR RBLK
	LDY #0
	LDA (TBUF)Y
	CMP #EOT
	BEQ FAH40
	CMP #BLF
	BEQ FAH50
	CMP #BDFH
	BNE FAH30
FAH50	TAX
	JSR TXTST
	BNE FAH45
	LDY #MS17-MS1
	JSR MSG
	LDY #5
FAH55	LDA (TBUF)Y
	JSR BSOUT
	INY
	CPY #21
	BNE FAH55
FAH45	LDY #1
FAH40	PLA
	STA VERCK
	TYA
	RTS
TAPEH	STA T1
	JSR ZZZ
	LDA STAH
	PHA
	LDA STAL
	PHA
	LDA EAH
	PHA
	LDA EAL
	PHA
	LDY #BUFSZ-1
	LDA #' 
BLNK2	STA (TBUF)Y
	DEY
	BNE BLNK2
	LDA T1
	STA (TBUF)Y
	INY
	LDA STAL
	STA (TBUF)Y
	INY
	LDA STAH
	STA (TBUF)Y
	INY
	LDA EAL
	STA (TBUF)Y
	INY
	LDA EAH
	STA (TBUF)Y
	INY
	STY T2
	LDY #0
	STY T1
TH20	LDY T1
	CPY FNLEN
	BEQ TH30
	LDA (FNADR)Y
	LDY T2
	STA (TBUF)Y
	INC T1
	INC T2
	BNE TH20
TH30	JSR LDAD1
	LDA #$69
	STA SHCNH
	JSR TWRT2
	PLA
	STA EAL
	PLA 
	STA EAH
	PLA
	STA STAL
	PLA
	STA STAH
	RTS
LDAD2	JSR TWAIT
	LDX #0
	LDY #1
LDAD3	LDA (TBUF)Y
	STA SAL,X
	INX
	INY
	CPX #4
	BNE LDAD3
	LDA SAL
	STA STAL
	LDA SAH
	STA STAH
	RTS
ZZZ	JSR ZZZGUY      ;GET DEVICE #
	LDA CASS1,X     ;CHK FOR ALLOCATION
	BNE ZZZ001
	LDA #192        ;ALLOCATE 192 BYTES
	STA CASS1,X     ;FLAG CASS ACTIVE
	JSR GETSPA      ;ALLOCATE SPACE
	JSR ZZZGUY
	LDA FRETOP      ;LO
	STA CASS1+1,X
	TYA             ;HI
	STA CASS1+2,X
ZZZ001	LDA CASS1+1,X   ;MOVE TO INDIRECTS
	STA TBUF
	LDA CASS1+2,X
	STA TBUF+1
	RTS
;
ZZZGUY	LDX #0          ;INDEX FOR CASS1
	LDA FA
	LSR A
	BCS ZZ10
	LDX #3          ;INDEX FOR CASS2
ZZ10	RTS
;
LDAD1	JSR TWAIT
	JSR ZZZ
	LDA TBUF
	STA STAL
	CLC
	ADC #BUFSZ
	STA EAL
	LDA TBUF+1
	STA STAH
	ADC #0
	STA EAH
	RTS
SYS	JSR FRMNUM
	JSR GETADR
	JMP (LINNUM)
SV60	LDA VARTAB
	STA EAL
	LDA VARTAB+1
	STA EAH
	LDA TXTTAB+1
	STA STAH
	LDA TXTTAB
	STA STAL
	RTS
SAVE	JSR PARS1
SV3	JSR SV60
SV5	LDA FA ***MONITOR ENTRY
	BNE SV20
SV10	LDX #ERR23
	JMP ERROR
SV20	CMP #3
	BEQ SV10
	BCC SV100
	LDA #$61
	STA SA
	LDY FNLEN
	BNE *+5
	JMP SNERR
	JSR OPENI
	JSR LISTN
	LDA SA
	JSR SECND
	LDY #0
	JSR RD300
	LDA SAL
	JSR CIOUT
	LDA SAH
	JSR CIOUT
SV30	JSR WRT62
	BEQ SV50
	LDA (SAL)Y
	JSR CIOUT
	JSR STOP
	INC SAL
	BNE SV30
	INC SAH
	BNE SV30
SV50	JSR UNLSN
CLSEI	BIT SA
	BMI UD65
	JSR LISTN
	LDA SA
	AND #$EF
	ORA #$E0
	JSR SECND
	JMP UNLSN
SV100	JSR ZZZ
	JSR CSTE2
	JSR TXTST
	BNE SV105
	LDY #MS11-MS1
	JSR MSG
	JSR LD105
SV105	LDA #BLF
	JSR TAPEH
	JSR TWRT
	LDA SA
	AND #2
	BEQ UD65
	LDA #EOT
	JMP TAPEH
UDTIM	INC CRFAC
	LDA CRFAC
	BNE UD10
	INC CRFAC+1
UD10	CMP #$6F
	BNE UD20
	LDA CRFAC+1
	CMP #2
	BEQ UD50
UD20	INC TIME+2
	BNE UD30
	INC TIME+1
	BNE UD30
	INC TIME
UD30	LDX #0
UD40	LDA TIME,X
	CMP UD70,X
	BCC UD60
	INX
	CPX #3
	BNE UD40
	LDA #0
UD45	STA TIME-1,X
	DEX
	BNE UD45
	BEQ UD60
UD50	LDA #0
	STA CRFAC
	STA CRFAC+1
UD60	LDA PIAK
	CMP PIAK
	BNE UD60
	STA STKEY
UD65	RTS
UD70	.BYT $4F,$1A,1
.END
